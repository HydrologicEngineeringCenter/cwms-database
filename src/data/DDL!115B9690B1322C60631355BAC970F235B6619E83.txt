
  CREATE OR REPLACE TYPE "CWMS_20"."STREAM_RATING_T" 
/**
 * Holds a USGS-style stream rating with shifts and offsets
 *
 * @see type rating_t
 *
 * @member offsets The logarithmic stage interpolation offsets, if any to use with the rating
 * @member shifts  The stage shifts, if any, to use with the rating
 */
under rating_t (
-- office_id      varchar2(16),
-- rating_spec_id varchar2(372),
-- effective_date date,
-- create_date    date,
-- active_flag    varchar2(1),
-- formula        varchar2(1000),
-- native_units   varchar2(256),
-- description    varchar2(256),
-- rating_info    rating_ind_parameter_t,
-- current_units  varchar2(1), -- 'D' = database, 'N' = native, other = don't know
-- current_time   varchar2(2), -- 'D' = database, 'L' = native, other = don't know
   offsets        rating_t,
   shifts         rating_tab_t,

   /**
    * Construct a stream_rating_t object from data in the database.
    *
    * @param p_rating_code The primary key of the AT_RATING table
    */
   constructor function stream_rating_t(
      p_rating_code in number)
   return self as result,
   /**
    * Construct a stream_rating_t object from data in the database.
    *
    * @param p_rating_spec_id The rating specification of the rating to construct
    * @param p_effective_date The effective date
    * @param p_match_date     A flag ('T' or 'F') specifying whether the p_effective_date parameter is to be matched exactly.  If 'F', the latest effective date on or before p_effective_date will be used.
    * @param p_time_zone      The time zone for p_effective_date.  If NULL, the local time zone of the rating's location will be used.
    * @param p_office_id      The office owning the rating.  If NULL, the session user's default office will be used
    */
   constructor function stream_rating_t(
      p_rating_id      in varchar2,
      p_effective_date in date     default null,
      p_match_date     in varchar2 default 'F',
      p_time_zone      in varchar2 default null,
      p_office_id      in varchar2 default null)
   return self as result,
   /**
    * Constructs a rating_t object from an XML instance.  The XML
    * instance must conform to the <a href="http://www.hec.usace.army.mil/xmlSchema/CWMS/Ratings.xsd">CWMS Ratings XML Schema</a>.
    * The instance structure is <a href="http://www.hec.usace.army.mil/xmlSchema/CWMS/Ratings.htm#element_usgs-stream-rating">documented here</a>.
    *
    * @param p_xml The XML instance
    */
   constructor function stream_rating_t(
      p_xml in xmltype)
   return self as result,
   -- not documented
   overriding member procedure init(
      p_rating_code in number),
   -- not documented
   overriding member procedure validate_obj,
   /**
    * Sets all rating values of this rating to database storage units, converting if necessary
    */
   overriding member procedure convert_to_database_units,
   /**
    * Sets all rating values of this rating to native units, converting if necessary
    */
   overriding member procedure convert_to_native_units,
   /**
    * Sets the times of this rating to UTC, converting if necessary
    */
   overriding member procedure convert_to_database_time,
   /**
    * Sets the times of this rating to the local time of the rating's location, converting if necessary
    */
   overriding member procedure convert_to_local_time,
   /**
    * Stores the rating to the database
    *
    * @param p_fail_if_exists A flag ('T' or 'F') that specifies whether the function
    *        should fail if the rating already exists in the database
    *
    * @exception ITEM_ALREADY_EXISTS if p_fail_if_exists is set to 'T' and the
    *            rating already exists
    */
   overriding member procedure store(
      p_fail_if_exists in varchar2),
   /**
    * Retrieves the rating as an XML instance in an CLOB object
    *
    * @return the rating as an XML instance in an CLOB object
    */
   overriding member function to_clob
   return clob,
   /**
    * Retrieves the rating as an XML instance in an XMLTYPE object
    *
    * @return the rating as an XML instance in an XMLTYPE object
    */
   overriding member function to_xml
   return xmltype,
   /**
    * Rate the specified independent values
    *
    * @param p_ind_values the values to rate
    *
    * @return the rated values
    */
   overriding member function rate(
      p_ind_values in double_tab_tab_t)
   return double_tab_t,
   /**
    * Rate the specified independent values
    *
    * @param p_ind_values the values to rate
    *
    * @return the rated values
    */
   overriding member function rate(
      p_ind_values in double_tab_t)
   return double_tab_t,
   /**
    * Rate the specified independent values
    *
    * @param p_ind_values the values to rate
    *
    * @return the rated values
    */
   overriding member function rate_one(
      p_ind_values in double_tab_t)
   return binary_double,
   /**
    * Rate the specified independent values
    *
    * @param p_ind_values the values to rate
    *
    * @return the rated values
    */
   overriding member function rate(
      p_ind_value in binary_double)
   return binary_double,
   /**
    * Rate the specified independent values
    *
    * @param p_ind_values the values to rate
    *
    * @return the rated values
    */
   overriding member function rate(
      p_ind_values in tsv_array)
   return tsv_array,
   /**
    * Rate the specified independent values
    *
    * @param p_ind_values the values to rate
    *
    * @return the rated values
    */
   overriding member function rate(
      p_ind_values in ztsv_array)
   return ztsv_array,
   /**
    * Rate the specified independent values
    *
    * @param p_ind_values the values to rate
    *
    * @return the rated values
    */
   overriding member function rate(
      p_ind_value in tsv_type)
   return tsv_type,
   /**
    * Rate the specified independent values
    *
    * @param p_ind_values the values to rate
    *
    * @return the rated values
    */
   overriding member function rate(
      p_ind_value in ztsv_type)
   return ztsv_type,
   /**
    * Reverse rate the specified dependent values
    *
    * @param p_dep_values the values to rate
    *
    * @return the rated values
    */
   overriding member function reverse_rate(
      p_dep_values in double_tab_t)
   return double_tab_t,
   /**
    * Reverse rate the specified dependent values
    *
    * @param p_dep_values the values to rate
    *
    * @return the rated values
    */
   overriding member function reverse_rate(
      p_dep_value in binary_double)
   return binary_double,
   /**
    * Reverse rate the specified dependent values
    *
    * @param p_dep_values the values to rate
    *
    * @return the rated values
    */
   overriding member function reverse_rate(
      p_dep_values in tsv_array)
   return tsv_array,
   /**
    * Reverse rate the specified dependent values
    *
    * @param p_dep_values the values to rate
    *
    * @return the rated values
    */
   overriding member function reverse_rate(
      p_dep_values in ztsv_array)
   return ztsv_array,
   /**
    * Reverse rate the specified dependent values
    *
    * @param p_dep_values the values to rate
    *
    * @return the rated values
    */
   overriding member function reverse_rate(
      p_dep_value in tsv_type)
   return tsv_type,
   /**
    * Reverse rate the specified dependent values
    *
    * @param p_dep_values the values to rate
    *
    * @return the rated values
    */
   overriding member function reverse_rate(
      p_dep_value in ztsv_type)
   return ztsv_type,
   -- not documented
   member procedure trim_to_effective_date(
      p_date_time in date),
   -- not documented
   member procedure trim_to_create_date(
      p_date_time in date),
   -- not documented
   member function latest_shift_date
   return date
);
CREATE OR REPLACE TYPE BODY "CWMS_20"."STREAM_RATING_T" 
as
   constructor function stream_rating_t(
      p_rating_code in number)
   return self as result
   is
   begin
      (self as rating_t).init(p_rating_code);
      self.init(p_rating_code);
      return;
   end;

   constructor function stream_rating_t(
      p_rating_id      in varchar2,
      p_effective_date in date     default null,
      p_match_date     in varchar2 default 'F',
      p_time_zone      in varchar2 default null,
      p_office_id      in varchar2 default null)
   return self as result
   is
      l_rating_code number(10);
   begin
      l_rating_code := rating_t.get_rating_code(
         p_rating_id,
         p_effective_date,
         p_match_date,
         p_time_zone,
         p_office_id);

      (self as rating_t).init(l_rating_code);
      self.init(l_rating_code);
      return;
   end;

   constructor function stream_rating_t(
      p_xml in xmltype)
   return self as result
   is
      l_xml              xmltype;
      l_node             xmltype;
      l_shift            xmltype;
      l_offsets          xmltype;
      l_rating_points    xmltype;
      l_point            xmltype;
      l_timestr          varchar2(32);
      l_location_id      varchar2(49);
      l_ind_param        varchar2(16);
      l_template_version varchar2(32);
      l_rating_version   varchar2(32);
      l_parts            str_tab_t;
      l_skipped          pls_integer;
      ------------------------------
      -- local function shortcuts --
      ------------------------------
      function get_node(p_xml in xmltype, p_path in varchar2) return xmltype is
      begin
         return cwms_util.get_xml_node(p_xml, p_path);
      end;
      function get_text(p_xml in xmltype, p_path in varchar2) return varchar2 is
      begin
         return cwms_util.get_xml_text(p_xml, p_path);
      end;
      function get_number(p_xml in xmltype, p_path in varchar2) return number is
      begin
         return cwms_util.get_xml_number(p_xml, p_path);
      end;
   begin
      ----------------------------
      -- get the rating element --
      ----------------------------
      l_xml := get_node(p_xml, '//usgs-stream-rating[1]');
      if l_xml is null then
         cwms_err.raise(
            'ERROR',
            'Cannot locate <usgs-stream-rating> element');
      end if;
      -----------------------
      -- get the office id --
      -----------------------
      self.office_id := get_text(l_xml, '/usgs-stream-rating/@office-id');
      if self.office_id is null then
         cwms_err.raise('ERROR', 'Required office-id attribute not found');
      end if;
      -------------------------
      -- get the rating spec --
      -------------------------
      self.rating_spec_id := get_text(l_xml, '/usgs-stream-rating/rating-spec-id');
      if self.rating_spec_id is null then
         cwms_err.raise('ERROR', 'Required <rating-spec-id> element not found');
      end if;
      l_parts             := cwms_util.split_text(self.rating_spec_id, cwms_rating.separator1);
      l_location_id       := l_parts(1);
      l_template_version  := l_parts(3);
      l_rating_version    := l_parts(4);
      l_parts             := cwms_util.split_text(l_parts(2), cwms_rating.separator2);
      l_ind_param         := cwms_util.get_base_id(l_parts(1));
      ----------------------------
      -- get the effective date --
      ----------------------------
      l_timestr := get_text(l_xml, '/usgs-stream-rating/effective-date');
      if l_timestr is null then
         cwms_err.raise('ERROR', 'Required <effective-date> element not found');
      end if;
      self.effective_date := (self as rating_t).get_date(l_timestr);
      -------------------------
      -- get the create date --
      -------------------------
      l_timestr := get_text(l_xml, '/usgs-stream-rating/create-date');
      if l_timestr is not null then
         self.create_date := (self as rating_t).get_date(l_timestr);
      end if;
      -------------------------
      -- get the active flag --
      -------------------------
      self.active_flag :=
         case get_text(l_xml, '/usgs-stream-rating/active')
            when 'true'  then 'T'
            when '1'     then 'T'
            when 'false' then 'F'
            when '0'     then 'F'
            else               null
         end;
      if self.active_flag is null then
         cwms_err.raise(
            'ERROR',
            '<active> element not found or contains invalid text ');
      end if;
      ----------------------
      -- get the units id --
      ----------------------
      self.native_units := get_text(l_xml, '/usgs-stream-rating/units-id');
      if self.native_units is null then
         cwms_err.raise('ERROR', 'Required <units-id> element not found');
      end if;
      -------------------------
      -- get the description --
      -------------------------
      self.description := get_text(l_xml, '/usgs-stream-rating/description');
      --------------------
      -- for each shift --
      --------------------
      l_skipped := 0;
      for i in 1..9999999 loop
         l_shift := get_node(l_xml, '/usgs-stream-rating/height-shifts['||i||']');
         exit when l_shift is null;
         ----------------------------------------------------
         -- create a new rating_t object to hold the shift --
         ----------------------------------------------------
         if i = 1 then
            self.shifts := rating_tab_t();
         end if;
         self.shifts.extend;
         self.shifts(i-l_skipped) := rating_t(
            self.office_id,              -- office_id
            l_location_id
            ||cwms_rating.separator1||l_ind_param
            ||cwms_rating.separator2||l_ind_param||'-Shift'
            ||cwms_rating.separator1||l_template_version
            ||cwms_rating.separator1||l_rating_version,     -- rating_spec_id
            null,                        -- effective_date
            null,                        -- create_date
            null,                        -- active_flag
            null,                        -- formula
            null,                        -- native_units
            null,                        -- description
            null,                        -- rating_info
            'N',                         -- current_units
            'D',                         -- current_time
            null);                       -- formula_tokens
         ----------------------------------
         -- get the shift effective date --
         ----------------------------------
         l_timestr := get_text(l_shift, '/height-shifts/effective-date');
         if l_timestr is null then
            cwms_err.raise('ERROR', 'Required <effective-date> element not found on shift');
         end if;
         self.shifts(i-l_skipped).effective_date := (self as rating_t).get_date(l_timestr);
         ----------------------------------
         -- get the shift create date --
         ----------------------------------
         l_timestr := get_text(l_shift, '/height-shifts/create-date');
         if l_timestr is not null then
            self.shifts(i-l_skipped).create_date := (self as rating_t).get_date(l_timestr);
         end if;
         -------------------------------
         -- get the shift active flag --
         -------------------------------
         self.shifts(i-l_skipped).active_flag :=
            case get_text(l_shift, '/height-shifts/active')
               when 'true'  then 'T'
               when '1'     then 'T'
               when 'false' then 'F'
               when '0'     then 'F'
               else               null
            end;
         if self.shifts(i-l_skipped).active_flag is null then
            cwms_err.raise(
               'ERROR',
               'Invalid text for <active> element: '
               ||get_text(l_shift, '/height-shifts/active'));
         end if;
         ----------------------------
         -- get the shift units id --
         ----------------------------
         l_parts := cwms_util.split_text(self.native_units, cwms_rating.separator2);
         self.shifts(i-l_skipped).native_units := l_parts(1) || cwms_rating.separator2 || l_parts(1);
         -------------------------------
         -- get the shift description --
         -------------------------------
         self.shifts(i-l_skipped).description := get_text(l_shift, '/height-shifts/description');
         --------------------------
         -- for each shift point --
         --------------------------
         for j in 1..9999999 loop
            l_point := get_node(l_shift, '/height-shifts/point['||j||']');
            exit when l_point is null;
            ------------------------------------------------------------
            -- create a new rating_value_t object for the shift point --
            ------------------------------------------------------------
            if j = 1 then
               self.shifts(i-l_skipped).rating_info := rating_ind_parameter_t(
                  'F',                  -- constructed
                  rating_value_tab_t(), -- rating_values
                  null);                -- extension_values
            end if;
            self.shifts(i-l_skipped).rating_info.rating_values.extend();
            self.shifts(i-l_skipped).rating_info.rating_values(j) := rating_value_t();
            self.shifts(i-l_skipped).rating_info.rating_values(j).ind_value := get_number(l_point, '/point/ind');
            self.shifts(i-l_skipped).rating_info.rating_values(j).dep_value := get_number(l_point, '/point/dep');
            self.shifts(i-l_skipped).rating_info.rating_values(j).note_id   := get_text(l_point, '/point/note');
         end loop;
         self.shifts(i-l_skipped).rating_info.constructed := 'T';
         begin
            self.shifts(i-l_skipped).rating_info.validate_obj(1);
            dbms_output.put_line('shift OK');
         exception
            when others then
               cwms_msg.log_db_message(
                  'stream_rating_t.store',
                  cwms_msg.msg_level_normal,
                  'Rating shift '||i||' skipped due to '||sqlerrm);
               l_skipped := l_skipped + 1;
               self.shifts.trim;
         end;
      end loop;
      l_offsets := get_node(l_xml, '/usgs-stream-rating/height-offsets');
      if l_offsets is not null then
         ------------------------------------------------------
         -- create a new rating_t object to hold the offsets --
         ------------------------------------------------------
         self.offsets := rating_t(
            self.office_id,                      -- office_id
            l_location_id
            ||cwms_rating.separator1||l_ind_param
            ||cwms_rating.separator2||l_ind_param||'-Offset'
            ||cwms_rating.separator1||l_template_version
            ||cwms_rating.separator1||l_rating_version,             -- rating_spec_id
            self.effective_date,                 -- effective_date
            self.create_date,                    -- create_date
            self.active_flag,                    -- active_flag
            null,                                -- formula
            null,                                -- native_units
            'Logarithmic interpolation offsets', -- description
            null,                                -- rating_info
            'N',                                 -- current_units
            'D',                                 -- current_time
            null);                               -- formula_tokens
         ----------------------------
         -- get the offset units id --
         ----------------------------
         l_parts := cwms_util.split_text(self.native_units, cwms_rating.separator2);
         self.offsets.native_units := l_parts(1) || cwms_rating.separator2 || l_parts(1);
         --------------------------
         -- for each offset point --
         --------------------------
         for i in 1..9999999 loop
            l_point := get_node(l_offsets, '/height-offsets/point['||i||']');
            exit when l_point is null;
            ------------------------------------------------------------
            -- create a new rating_value_t object for the offset point --
            ------------------------------------------------------------
            if i = 1 then
               self.offsets.rating_info := rating_ind_parameter_t(
                  'F',                  -- constructed
                  rating_value_tab_t(), -- rating_values
                  null);                -- extension_values
            end if;
            self.offsets.rating_info.rating_values.extend();
            self.offsets.rating_info.rating_values(i) := rating_value_t();
            self.offsets.rating_info.rating_values(i).ind_value := get_number(l_point, '/point/ind');
            self.offsets.rating_info.rating_values(i).dep_value := get_number(l_point, '/point/dep');
            self.offsets.rating_info.rating_values(i).note_id   := get_text(l_point, '/point/note');
         end loop;
         if self.offsets is not null then
            self.offsets.rating_info.constructed := 'T';
            begin
               self.offsets.rating_info.validate_obj(1);
            exception
               when others then
                  cwms_msg.log_db_message(
                     'stream_rating_t.store',
                     cwms_msg.msg_level_normal,
                     'Rating offsets error '||sqlerrm);
                  raise;
            end;
         end if;
      end if;
      l_rating_points := get_node(l_xml, '/usgs-stream-rating/rating-points');
      if l_rating_points is not null then
         ---------------------------
         -- for each rating point --
         ---------------------------
         for i in 1..9999999 loop
            l_point := get_node(l_rating_points, '/rating-points/point['||i||']');
            exit when l_point is null;
            -------------------------------------------------------------
            -- create a new rating_value_t object for the rating point --
            -------------------------------------------------------------
            if i = 1 then
               self.rating_info := rating_ind_parameter_t(
                  'F',                  -- constructed
                  rating_value_tab_t(), -- rating_values
                  null);                -- extension_values
            end if;
            self.rating_info.rating_values.extend();
            self.rating_info.rating_values(i) := rating_value_t();
            self.rating_info.rating_values(i).ind_value := get_number(l_point, '/point/ind');
            self.rating_info.rating_values(i).dep_value := get_number(l_point, '/point/dep');
            self.rating_info.rating_values(i).note_id   := get_text(l_point, '/point/note');
         end loop;
         if self.rating_info is not null then
            self.rating_info.constructed := 'T';
            self.rating_info.validate_obj(1);
         end if;
      end if;
      self.current_units := 'N';
      self.current_time := 'D';
      self.validate_obj;
      return;
   end;

   overriding member procedure init(
      p_rating_code in number)
   is
      l_shifts_codes number_tab_t := number_tab_t();
      l_offsets_code number(10);
      l_time_zone    varchar2(28);
   begin
      begin
         select r.rating_code bulk collect
           into l_shifts_codes
           from at_rating r,
                at_rating_spec rs,
                at_rating_template rt
          where ref_rating_code = p_rating_code
            and rs.rating_spec_code = r.rating_spec_code
            and rt.template_code = rs.template_code
            and rt.parameters_id = 'Stage;Stage-Shift'
       order by r.effective_date;
      exception
         when no_data_found then null;
      end;

      if l_shifts_codes.count > 0 then
         select tz.time_zone_name
           into l_time_zone
           from at_rating r,
                at_rating_spec rs,
                at_physical_location pl,
                cwms_time_zone tz
          where r.rating_code = p_rating_code
            and rs.rating_spec_code = r.rating_spec_code
            and pl.location_code = rs.location_code
            and tz.time_zone_code = nvl(pl.time_zone_code, 0);

         if l_time_zone = 'Unknown or Not Applicable' then
            l_time_zone := 'UTC';
         end if;

         self.shifts := rating_tab_t();
         self.shifts.extend(l_shifts_codes.count);
         for i in 1..l_shifts_codes.count loop
            self.shifts(i) := rating_t(l_shifts_codes(i));
            self.shifts(i).effective_date := cwms_util.change_timezone(
               self.shifts(i).effective_date,
               'UTC',
               l_time_zone);
            self.shifts(i).create_date := cwms_util.change_timezone(
               self.shifts(i).create_date,
               'UTC',
               l_time_zone);
         end loop;
      end if;

      begin
         select r.rating_code
           into l_offsets_code
           from at_rating r,
                at_rating_spec rs,
                at_rating_template rt
          where ref_rating_code = p_rating_code
            and rs.rating_spec_code = r.rating_spec_code
            and rt.template_code = rs.template_code
            and rt.parameters_id = 'Stage;Stage-Offset';

         self.offsets := rating_t(l_offsets_code);
         self.offsets.effective_date := self.effective_date;
         self.offsets.create_date    := self.create_date;
      exception
         when no_data_found then null;
      end;
      self.validate_obj;
   end;

   overriding member procedure validate_obj
   is
      l_parts         str_tab_t;
      l_ind_param     varchar2(256);
      l_dep_param     varchar2(256);
      l_parameters_id varchar2(256);
   begin
      ------------------------
      -- validate as rating --
      ------------------------
      (self as rating_t).validate_obj;
      ------------------------------------------------------
      -- validate Stage;Flow or Elev;Flow base parameters --
      ------------------------------------------------------
      l_parts := cwms_util.split_text(self.rating_spec_id, cwms_rating.separator1);
      l_parts := cwms_util.split_text(l_parts(2), cwms_rating.separator2);
      l_ind_param := l_parts(1);
      l_dep_param := l_parts(2);
      if instr(l_ind_param, cwms_rating.separator3) != 0 or
         (cwms_util.get_base_id(l_ind_param) != 'Stage' and
          cwms_util.get_base_id(l_ind_param) != 'Elev') or
          cwms_util.get_base_id(l_dep_param) != 'Flow'
      then
         l_parts := cwms_util.split_text(self.rating_spec_id, cwms_rating.separator1);
         cwms_err.raise(
            'ERROR',
            'Invalid parameters identifier for stream rating: '||l_parts(2));
      end if;
      l_ind_param := cwms_util.get_base_id(l_ind_param);
      ----------------------
      -- validate offsets --
      ----------------------
      if self.offsets is not null then
         begin
            self.offsets.validate_obj;
            if self.offsets.office_id != self.office_id then
               cwms_err.raise('ERROR', 'Offsets office does not match rating office');
            end if;
            l_parts := cwms_util.split_text(self.offsets.rating_spec_id, cwms_rating.separator1);
            l_parameters_id := l_parts(2);
            if l_parameters_id != l_ind_param || cwms_rating.separator2 || l_ind_param || '-Offset' then
               cwms_err.raise('ERROR', 'Invalid offsets parameter id - should be '||l_ind_param||cwms_rating.separator2||l_ind_param||'-Offset');
            end if;
            if self.offsets.effective_date != self.effective_date then
               cwms_err.raise('ERROR', 'Offsets effective date does not match rating effective date');
            end if;
            if (self.offsets.create_date is null) != (self.create_date is null) then
               cwms_err.raise('ERROR', 'Offsets create date does not match rating create date');
            end if;
            if self.create_date is not null then
               if self.offsets.create_date != self.create_date then
                  cwms_err.raise('ERROR', 'Offsets create date does not match rating create date');
               end if;
            end if;
            if self.offsets.formula is not null then
               cwms_err.raise('ERROR', 'Offsets cannot use a formula');
            end if;
            if self.offsets.native_units is null then
               cwms_err.raise('ERROR', 'Offsets must use same unit as rating stage or elevation unit');
            end if;
            l_parts := cwms_util.split_text(self.offsets.native_units, cwms_rating.separator2);
            if l_parts.count != 2 or l_parts(1) != l_parts(2) then
               cwms_err.raise('ERROR', 'Invalid native units for offsets');
            end if;
            if substr(self.native_units, 1, instr(self.native_units, cwms_rating.separator2) - 1) != l_parts(1) then
               cwms_err.raise('ERROR', 'Offsets must use same unit as rating stage or elevation unit');
            end if;
            if self.offsets.rating_info.extension_values is not null then
               cwms_err.raise('ERROR', 'Offsets cannot contain extension values');
            end if;
            if self.offsets.rating_info.rating_values is null then
               cwms_err.raise('ERROR', 'Offsets must contain rating values if specified');
            end if;
            for i in 1..self.offsets.rating_info.rating_values.count loop
               if i > 1 then
                  if self.offsets.rating_info.rating_values(i).ind_value <=
                     self.offsets.rating_info.rating_values(i-1).ind_value
                  then
                     cwms_err.raise(
                        'ERROR',
                        'Offsets stages/elevations do not monotonically increase after value '
                        ||cwms_rounding.round_dt_f(self.offsets.rating_info.rating_values(i-1).ind_value, '9999999999'));
                  end if;
               end if;
               if self.offsets.rating_info.rating_values(i).dep_value is null or
                  self.offsets.rating_info.rating_values(i).dep_rating_ind_param is not null
               then
                  cwms_err.raise('ERROR', 'Offsets must contain offset values as dependent parameter');
               end if;
            end loop;
         exception
            when others then
               cwms_msg.log_db_message(
                  'stream_rating_t.store',
                  cwms_msg.msg_level_normal,
                  'Rating offsets error '||sqlerrm);
               raise;
         end;
      end if;
      ---------------------
      -- validate shifts --
      ---------------------
      if self.shifts is not null then
         for i in reverse 1..self.shifts.count loop
            begin
               self.shifts(i).validate_obj;
               if self.shifts(i).office_id != self.office_id then
                  cwms_err.raise('ERROR', 'Shifts office does not match rating office');
               end if;
               l_parts := cwms_util.split_text(self.shifts(i).rating_spec_id, cwms_rating.separator1);
               l_parameters_id := l_parts(2);
               if l_parameters_id != l_ind_param || cwms_rating.separator2 || l_ind_param || '-Shift' then
                  cwms_err.raise('ERROR', 'Invalid shift parameter id - should be '||l_ind_param||cwms_rating.separator2||l_ind_param||'-Shift');
               end if;
               if self.shifts(i).effective_date < self.effective_date then
                  cwms_err.raise(
                     'ERROR',
                     'Shift '||i||' effective date ('
                     ||self.shifts(i).effective_date
                     ||') is earlier than rating effective date ('
                     ||self.effective_date
                     ||')');
               end if;
               if self.shifts(i).create_date is not null then
                  if self.create_date is null or self.shifts(i).create_date < self.create_date then
                     cwms_err.raise(
                        'ERROR',
                        'Shift '||i||' create date ('
                        ||self.shifts(i).create_date
                        ||') is earlier than rating create date ('
                        ||self.create_date
                        ||')');
                  end if;
               end if;
               if self.shifts(i).formula is not null then
                  cwms_err.raise('ERROR', 'Shifts cannot use a formula');
               end if;
               if self.shifts(i).native_units is null then
                  cwms_err.raise('ERROR', 'Shifts must use same unit as rating stage or elevation unit');
               end if;
               l_parts := cwms_util.split_text(self.shifts(i).native_units, cwms_rating.separator2);
               if l_parts.count != 2 or l_parts(1) != l_parts(2) then
                  cwms_err.raise('ERROR', 'Invalid native units for shifts');
               end if;
               if substr(self.native_units, 1, instr(self.native_units, cwms_rating.separator2) - 1) != l_parts(1) then
                  cwms_err.raise('ERROR', 'Shifts must use same unit as rating stage or elevation unit');
               end if;
               if self.shifts(i).rating_info.extension_values is not null then
                  cwms_err.raise('ERROR', 'Shifts cannot contain extension values');
               end if;
               if self.shifts(i).rating_info.rating_values is null then
                  cwms_err.raise('ERROR', 'Shifts must contain rating values if specified');
               end if;
               for j in 1..self.shifts(i).rating_info.rating_values.count loop
                  if j > 1 then
                     if self.shifts(i).rating_info.rating_values(j).ind_value <=
                        self.shifts(i).rating_info.rating_values(j-1).ind_value
                     then
                        cwms_err.raise(
                           'ERROR',
                           'Shifts stages/elevations do not monotonically increase after value '
                           ||cwms_rounding.round_dt_f(self.shifts(i).rating_info.rating_values(j-1).ind_value, '9999999999'));
                     end if;
                  end if;
                  if self.shifts(i).rating_info.rating_values(j).dep_value is null or
                     self.shifts(i).rating_info.rating_values(j).dep_rating_ind_param is not null
                  then
                     cwms_err.raise('ERROR', 'Shifts must contain shift values as dependent parameter');
                  end if;
               end loop;
            exception
               when others then
                  cwms_msg.log_db_message(
                     'stream_rating_t.store',
                     cwms_msg.msg_level_normal,
                     'Rating shift '||i||' skipped due to '||sqlerrm);
                  for j in i+1..self.shifts.count loop
                     self.shifts(j-1) := self.shifts(j);
                     self.shifts.trim(1);
                  end loop;
            end;
         end loop;
      end if;
   end;

   overriding member procedure convert_to_database_units
   is
   begin
      (self as rating_t).convert_to_database_units;
      if self.offsets is not null then
         self.offsets.convert_to_database_units;
      end if;
      if self.shifts is not null then
         for i in 1..self.shifts.count loop
            self.shifts(i).convert_to_database_units;
         end loop;
      end if;
   end;

   overriding member procedure convert_to_native_units
   is
   begin
      (self as rating_t).convert_to_native_units;
      if self.offsets is not null then
         self.offsets.convert_to_native_units;
      end if;
      if self.shifts is not null then
         for i in 1..self.shifts.count loop
            self.shifts(i).convert_to_native_units;
         end loop;
      end if;
   end;

   overriding member procedure convert_to_database_time
   is
   begin
      (self as rating_t).convert_to_database_time;
      if self.offsets is not null then
         self.offsets.convert_to_database_time;
      end if;
      if self.shifts is not null then
         for i in 1..self.shifts.count loop
            self.shifts(i).convert_to_database_time;
         end loop;
      end if;
   end;

   overriding member procedure convert_to_local_time
   is
   begin
      (self as rating_t).convert_to_local_time;
      if self.offsets is not null then
         self.offsets.convert_to_local_time;
      end if;
      if self.shifts is not null then
         for i in 1..self.shifts.count loop
            self.shifts(i).convert_to_local_time;
         end loop;
      end if;
   end;

   overriding member procedure store(
      p_fail_if_exists in varchar2)
   is
      l_rating_code      number(10);
      l_ref_rating_code  number(10);
      l_template         rating_template_t;
      l_parts            str_tab_t;
      l_location_id      varchar2(49);
      l_ind_param        varchar2(16);
      l_template_version varchar2(32);
      l_spec_version     varchar2(23);
      l_spec             rating_spec_t;
      l_rating_spec      rating_spec_t;
      l_clone            stream_rating_t;
   begin
      if self.current_units = 'N' or self.current_time = 'L' then
         l_clone := self;
         l_clone.convert_to_database_units;
         l_clone.convert_to_database_time;
         l_clone.store(p_fail_if_exists);
         return;
      end if;
      l_parts             := cwms_util.split_text(self.rating_spec_id, cwms_rating.separator1);
      l_location_id       := l_parts(1);
      l_template_version  := l_parts(3);
      l_spec_version      := l_parts(4);
      l_parts             := cwms_util.split_text(l_parts(2), cwms_rating.separator2);
      l_ind_param         := cwms_util.get_base_id(l_parts(1));
      l_rating_spec       := rating_spec_t(self.rating_spec_id, self.office_id);
      (self as rating_t).store(l_ref_rating_code, p_fail_if_exists);
      if self.shifts is not null then
        l_template := rating_template_t(
            self.office_id,
            l_ind_param||cwms_rating.separator2||l_ind_param||'-Shift',
            l_template_version,
            rating_ind_par_spec_tab_t(
               rating_ind_param_spec_t(
                  1,
                  l_ind_param,
                  'LINEAR',
                  'NEAREST',
                  'NEAREST')),
            l_ind_param||'-Shift',
            'USGS-style rating shifts');
         l_template.store('F');
         l_spec := rating_spec_t(
            self.office_id,
            l_location_id,
            l_template.parameters_id||cwms_rating.separator1||l_template.version,
            l_spec_version,
            l_rating_spec.source_agency_id,
            'LINEAR',
            'LINEAR',
            'NEAREST',
            l_rating_spec.active_flag,
            l_rating_spec.auto_update_flag,
            l_rating_spec.auto_activate_flag,
            'F',
            str_tab_t(l_rating_spec.ind_rounding_specs(1)),
            l_rating_spec.ind_rounding_specs(1),
            'USGS-style rating shifts');
         l_spec.store('F');
         for i in 1..self.shifts.count loop
            self.shifts(i).store(l_rating_code, 'F');
            update at_rating
               set ref_rating_code = l_ref_rating_code
             where rating_code = l_rating_code;
         end loop;
      end if;
      if self.offsets is not null then
         l_template := rating_template_t(
            self.office_id,
            l_ind_param||cwms_rating.separator2||l_ind_param||'-Offset',
            l_template_version,
            rating_ind_par_spec_tab_t(
               rating_ind_param_spec_t(
                  1,
                  l_ind_param,
                  'PREVIOUS',
                  'NEAREST',
                  'NEAREST')),
            l_ind_param||'-Offset',
            'USGS-style logarithmic interpolation offsets');
         l_template.store('F');
         l_spec := rating_spec_t(
            self.office_id,
            l_location_id,
            l_template.parameters_id||cwms_rating.separator1||l_template.version,
            l_spec_version,
            l_rating_spec.source_agency_id,
            'NEAREST',
            'PREVIOUS',
            'NEAREST',
            l_rating_spec.active_flag,
            l_rating_spec.auto_update_flag,
            l_rating_spec.auto_activate_flag,
            'F',
            str_tab_t(l_rating_spec.ind_rounding_specs(1)),
            l_rating_spec.ind_rounding_specs(1),
            'USGS-style logarithmic interpolation offsets');
         l_spec.store('F');
         self.offsets.store(l_rating_code, 'F');
         update at_rating
            set ref_rating_code = l_ref_rating_code
          where rating_code = l_rating_code;
      end if;
   end;

   overriding member function to_clob
   return clob
   is
      l_text  clob;
      l_clone stream_rating_t;
      function bool_text(
         p_state in boolean)
      return varchar2
      is
      begin
         return case p_state
                   when true  then 'true'
                   when false then 'false'
                end;
      end;
   begin
      if self.current_units = 'D' or self.current_time = 'D' then
         l_clone := self;
         l_clone.convert_to_native_units;
         l_clone.convert_to_local_time;
         return l_clone.to_clob;
      end if;
      dbms_lob.createtemporary(l_text, true);
      dbms_lob.open(l_text, dbms_lob.lob_readwrite);
      cwms_util.append(l_text,
         '<usgs-stream-rating office-id="'||self.office_id||'">'
         ||'<rating-spec-id>'||self.rating_spec_id||'</rating-spec-id>'
         ||'<units-id>'||self.native_units||'</units-id>'
         ||'<effective-date>'||to_char(self.effective_date, 'yyyy-mm-dd"T"hh24:mi:ss')||'</effective-date>');
      if self.create_date is not null then
         cwms_util.append(l_text, '<create-date>'||to_char(self.create_date, 'yyyy-mm-dd"T"hh24:mi:ss')||'</create-date>');
      end if;
      cwms_util.append(l_text,
         '<active>'
         ||bool_text(cwms_util.is_true(self.active_flag))
         ||'</active>');
      if self.description is not null then
         cwms_util.append(l_text, '<description>'||self.description||'</description>');
      end if;
      -------------------
      -- output shifts --
      -------------------
      if self.shifts is not null then
         for i in 1..self.shifts.count loop
            cwms_util.append(l_text,
               '<height-shifts><effective-date>'
               ||to_char(self.shifts(i).effective_date, 'yyyy-mm-dd"T"hh24:mi:ss')||'</effective-date>');
            if self.shifts(i).create_date is not null then
               cwms_util.append(l_text, '<create-date>'||to_char(self.shifts(i).create_date, 'yyyy-mm-dd"T"hh24:mi:ss')||'</create-date>');
            end if;
            cwms_util.append(l_text,
               '<active>'
               ||bool_text(cwms_util.is_true(self.shifts(i).active_flag))
               ||'</active>');
            if self.shifts(i).description is not null then
               cwms_util.append(l_text, '<description>'||self.shifts(i).description||'</description>');
            end if;
            for j in 1..self.shifts(i).rating_info.rating_values.count loop
               cwms_util.append(l_text,
                  '<point><ind>'
                  ||cwms_rounding.round_dt_f(self.shifts(i).rating_info.rating_values(j).ind_value, '9999999999')
                  ||'</ind><dep>'
                  ||cwms_rounding.round_dt_f(self.shifts(i).rating_info.rating_values(j).dep_value, '9999999999')
                  ||'</dep>');
               if self.shifts(i).rating_info.rating_values(j).note_id is not null then
                  cwms_util.append(l_text,
                     '<note>'
                     ||self.shifts(i).rating_info.rating_values(j).note_id
                     ||'</note>');
               end if;
               cwms_util.append(l_text, '</point>');
            end loop;
         end loop;
         cwms_util.append(l_text, '</height-shifts>');
      end if;
      -------------------
      -- output offsets -
      -------------------
      if self.offsets is not null then
         cwms_util.append(l_text, '<height-offsets>');
         for i in 1..self.offsets.rating_info.rating_values.count loop
            cwms_util.append(l_text,
               '<point><ind>'
               ||cwms_rounding.round_dt_f(self.offsets.rating_info.rating_values(i).ind_value, '9999999999')
               ||'</ind><dep>'
               ||cwms_rounding.round_dt_f(self.offsets.rating_info.rating_values(i).dep_value, '9999999999')
               ||'</dep>');
            if self.offsets.rating_info.rating_values(i).note_id is not null then
               cwms_util.append(l_text,
                  '<note>'
                  ||self.offsets.rating_info.rating_values(i).note_id
                  ||'</note>');
            end if;
            cwms_util.append(l_text, '</point>');
            end loop;
         cwms_util.append(l_text, '</height-offsets>');
      end if;
      -------------------
      -- rating points --
      -------------------
      cwms_util.append(l_text, '<rating-points>');
      for i in 1..self.rating_info.rating_values.count loop
         cwms_util.append(l_text,
            '<point><ind>'
            ||cwms_rounding.round_dt_f(self.rating_info.rating_values(i).ind_value, '9999999999')
            ||'</ind><dep>'
            ||cwms_rounding.round_dt_f(self.rating_info.rating_values(i).dep_value, '9999999999')
            ||'</dep>');
         if self.rating_info.rating_values(i).note_id is not null then
            cwms_util.append(l_text,
               '<note>'
               ||self.rating_info.rating_values(i).note_id
               ||'</note>');
         end if;
         cwms_util.append(l_text, '</point>');
      end loop;
      cwms_util.append(l_text, '</rating-points></usgs-stream-rating>');
      dbms_lob.close(l_text);
      return l_text;
   end;

   overriding member function to_xml
   return xmltype
   is
   begin
      return xmltype(self.to_clob());
   end;

   overriding member function rate(
      p_ind_values in double_tab_tab_t)
   return double_tab_t
   is
   begin
      if p_ind_values is null then
         return null;
      else
         if p_ind_values.count != 1 then
            cwms_err.raise(
               'ERROR',
               'Rating '
               ||rating_spec_id
               ||' takes 1 independent parameter, '
               ||p_ind_values.count
               ||' specified');
         end if;
         return rate(p_ind_values(1));
      end if;
   end;

   overriding member function rate(
      p_ind_values in double_tab_t)
   return double_tab_t
   is
      l_results double_tab_t;
      l_ztsv    ztsv_array;
   begin
      if p_ind_values is not null then
         l_ztsv := ztsv_array();
         l_ztsv.extend(p_ind_values.count);
         for i in 1..p_ind_values.count loop
            l_ztsv(i).date_time := sysdate;
            l_ztsv(i).value     := p_ind_values(i);
         end loop;
         l_ztsv := rate(l_ztsv);
         l_results := double_tab_t();
         l_results.extend(p_ind_values.count);
         for i in 1..p_ind_values.count loop
            l_results(i) := l_ztsv(i).value;
         end loop;
      end if;
      return l_results;
   end;

   overriding member function rate_one(
      p_ind_values in double_tab_t)
   return binary_double
   is
   begin
      if p_ind_values.count != 1 then
         cwms_err.raise(
            'ERROR',
            'Rating '
            ||rating_spec_id
            ||' takes 1 independent parameter, '
            ||p_ind_values.count
            ||' specified');
      end if;
      return rate(p_ind_values(1));
   end;

   overriding member function rate(
      p_ind_value in binary_double)
   return binary_double
   is
      l_ztsv ztsv_type;
   begin
      l_ztsv := rate(ztsv_type(sysdate, p_ind_value, 0));
      return l_ztsv.value;
   end;

   overriding member function rate(
      p_ind_values in tsv_array)
   return tsv_array
   is
      l_results tsv_array;
      l_ztsv    ztsv_array;
      l_clone   stream_rating_t;
   begin
      if p_ind_values is not null then
         l_ztsv := ztsv_array();
         l_ztsv.extend(p_ind_values.count);
         for i in 1..p_ind_values.count loop
            l_ztsv(i).date_time    := cast(p_ind_values(i).date_time at time zone 'UTC' as date);
            l_ztsv(i).value        := p_ind_values(i).value;
            l_ztsv(i).quality_code := 0;
         end loop;
         if current_time = 'D' then
            l_ztsv := rate(l_ztsv);
         else
            l_clone := self;
            l_clone.convert_to_database_time;
            l_ztsv := l_clone.rate(l_ztsv);
         end if;
         l_results := tsv_array();
         l_results.extend(p_ind_values.count);
         for i in 1..p_ind_values.count loop
            l_results(i).date_time    := p_ind_values(i).date_time;
            l_results(i).value        := l_ztsv(i).value;
            l_results(i).quality_code := case l_results(i) is null
                                            when true  then 5
                                            when false then 0
                                         end;
         end loop;
      end if;
      return l_results;
   end;

   overriding member function rate(
      p_ind_values in ztsv_array)
   return ztsv_array
   is
      type integer_tab_t is table of pls_integer;
      c_base_date               constant date := '1900-01-01';
      l_results                 ztsv_array;
      l_date_offsets            double_tab_t;
      l_date_offset             binary_double;
      l_ratio                   binary_double;
      l_date_offsets_properties cwms_lookup.sequence_properties_t;
      l_shift                   binary_double;
      l_offset                  binary_double;
      l_heights                 double_tab_t;
      l_flows                   double_tab_t;
      l_height                  binary_double;
      l_heights_properties      cwms_lookup.sequence_properties_t;
      i                         pls_integer;
      j                         pls_integer;
      k                         pls_integer;
      l_hi_index                pls_integer;
      l_hi_value                binary_double;
      l_lo_value                binary_double;
      l_hi_height               binary_double;
      l_lo_height               binary_double;
      l_hi_flow                 binary_double;
      l_lo_flow                 binary_double;
      l_min_height              binary_double;
      l_log_used                boolean;
      l_rating_spec             rating_spec_t;
      l_rating_method           pls_integer;
   begin
      if p_ind_values is not null then
         -------------------------
         -- get the rating spec --
         -------------------------
         l_rating_spec := rating_spec_t(rating_spec_id, office_id);
         -----------------------------------------
         -- populate the shift dates for lookup --
         -----------------------------------------
         if shifts is not null then
            l_date_offsets := double_tab_t();
            l_date_offsets.extend(shifts.count+1);
            l_date_offsets(1) := effective_date - c_base_date;
            for i in 1..shifts.count loop
               l_date_offsets(i+1) := shifts(i+1).effective_date - c_base_date;
            end loop;
            l_date_offsets_properties := cwms_lookup.analyze_sequence(l_date_offsets);
         end if;
         --------------------------------------------
         -- populate the rating heights for lookup --
         --------------------------------------------
         i := 1;
         j := 1;
         k := 0;
         l_heights := double_tab_t();
         l_flows   := double_tab_t();
         -------------------------------------------------
         -- first any extension values below the rating --
         -------------------------------------------------
         if rating_info.extension_values is not null then
            while i < rating_info.extension_values.count and
                  rating_info.extension_values(i).ind_value < rating_info.rating_values(1).ind_value
            loop
               l_heights.extend;
               l_flows.extend;
               k := k + 1;
               l_heights(k) := rating_info.extension_values(i).ind_value;
               l_flows(k) := rating_info.extension_values(i).dep_value;
            end loop;
         end if;
         ----------------------------
         -- next the rating values --
         ----------------------------
         while j < rating_info.rating_values.count loop
            l_heights.extend;
            l_flows.extend;
            k := k + 1;
            l_heights(k) := rating_info.rating_values(j).ind_value;
            l_flows(k) := rating_info.rating_values(j).dep_value;
         end loop;
         ---------------------------------------------------
         -- finally any extension values above the rating --
         ---------------------------------------------------
         if rating_info.extension_values is not null then
            while i < rating_info.extension_values.count loop
               if rating_info.extension_values(i).ind_value >
                  rating_info.rating_values(rating_info.rating_values.count).ind_value
               then
                  l_heights.extend;
                  l_flows.extend;
                  k := k + 1;
                  l_heights(k) := rating_info.extension_values(i).ind_value;
                  l_flows(k) := rating_info.extension_values(i).dep_value;
               end if;
            end loop;
         end if;
         l_heights_properties := cwms_lookup.analyze_sequence(l_heights);
         -------------------------
         -- process each height --
         -------------------------
         l_results := ztsv_array();
         l_results.extend(p_ind_values.count);
         for i in 1..p_ind_values.count loop
            -----------------------------------
            -- shift the height if necessary --
            -----------------------------------
            l_height := p_ind_values(i).value;
            if shifts is not null and p_ind_values(i).date_time >= effective_date then
               l_date_offset := p_ind_values(i).date_time - c_base_date;
               l_hi_index := cwms_lookup.find_high_index(
                  l_date_offset,
                  l_date_offsets,
                  l_date_offsets_properties);
               l_ratio := cwms_lookup.find_ratio(
                  l_log_used,
                  l_date_offset,
                  l_date_offsets,
                  l_hi_index,
                  l_date_offsets_properties.increasing_range,
                  cwms_lookup.method_linear,
                  cwms_lookup.method_error,
                  cwms_lookup.method_nearest);
               if l_ratio != 0. then
                  l_hi_value := shifts(l_hi_index+1).rate(l_height);
               end if;
               if l_ratio != 1. then
                  if l_hi_index = 1 then
                     l_lo_value := 0.;
                  else
                     l_lo_value := shifts(l_hi_index).rate(l_height);
                  end if;
               end if;
               if l_ratio = 0. then
                  l_height := l_height + l_lo_value;
               elsif l_ratio = 1. then
                  l_height := l_height + l_hi_value;
               else
                  l_height := l_height + l_lo_value + l_ratio * (l_hi_value - l_lo_value);
               end if;
            end if;
            -----------------------------------
            -- find the interpolation values --
            -----------------------------------
            l_hi_index := cwms_lookup.find_high_index(
               l_height,
               l_heights,
               l_heights_properties);
            if l_height < l_heights(1) then
               l_rating_method := cwms_lookup.method_by_name(l_rating_spec.out_range_low_rating_method);
            elsif l_height > l_heights(l_heights.count) then
               l_rating_method := cwms_lookup.method_by_name(l_rating_spec.out_range_high_rating_method);
            else
               l_rating_method := cwms_lookup.method_by_name(l_rating_spec.in_range_rating_method);
            end if;
            if l_rating_method in (cwms_lookup.method_logarithmic, cwms_lookup.method_log_lin) then
               if offsets is null then
                  l_offset := 0;
               else
                  l_min_height  := least(l_height, l_heights(l_hi_index-1));
                  l_offset := offsets.rate(l_min_height);
               end if;
               begin
                  l_lo_height := log(10, l_heights(l_hi_index-1) - l_offset);
                  l_hi_height := log(10, l_heights(l_hi_index) - l_offset);
                  if l_rating_method = cwms_lookup.method_logarithmic then
                     l_lo_flow   := log(10, l_flows(l_hi_index-1));
                     l_hi_flow   := log(10, l_flows(l_hi_index));
                  end if;
                  l_height    := log(10, l_height - l_offset);
                  l_log_used  := true;
               exception
                  when others then
                     l_lo_height := l_heights(l_hi_index-1);
                     l_hi_height := l_heights(l_hi_index);
                     l_lo_flow   := l_flows(l_hi_index-1);
                     l_hi_flow   := l_flows(l_hi_index);
                     l_log_used  := false;
               end;
            elsif l_rating_method = cwms_lookup.method_lin_log then
               l_lo_height := l_heights(l_hi_index-1);
               l_hi_height := l_heights(l_hi_index);
               begin
                  l_lo_flow   := log(10, l_flows(l_hi_index-1));
                  l_hi_flow   := log(10, l_flows(l_hi_index));
                  l_log_used  := true;
               exception
                  when others then
                     l_lo_flow   := l_flows(l_hi_index-1);
                     l_hi_flow   := l_flows(l_hi_index);
                     l_log_used  := false;
               end;
            else
               l_lo_height := l_heights(l_hi_index-1);
               l_hi_height := l_heights(l_hi_index);
               l_lo_flow   := l_flows(l_hi_index-1);
               l_hi_flow   := l_flows(l_hi_index);
               l_log_used  := false;
            end if;
            -------------------------------
            -- perform the interpolation --
            -------------------------------
            if l_rating_method in (
               cwms_lookup.method_linear,
               cwms_lookup.method_logarithmic,
               cwms_lookup.method_lin_log,
               cwms_lookup.method_log_lin)
            then
               l_results(i).value :=
                  l_lo_flow
                  + (l_height - l_lo_height)
                  / (l_hi_height - l_lo_height)
                  * (l_hi_flow - l_lo_flow);
               if l_log_used then
                  l_results(i).value := power(10, l_results(i).value);
               end if;
            elsif l_rating_method = cwms_lookup.method_null then
               l_results(i).value := null;
            elsif l_rating_method = cwms_lookup.method_error then
               if l_height < l_lo_height then
                  cwms_err.raise(
                     'ERROR',
                     'Value is out of bounds low');
               elsif l_height > l_hi_height then
                  cwms_err.raise(
                     'ERROR',
                     'Value is out of bounds high');
               else
                  cwms_err.raise(
                     'ERROR',
                     'Value does not match any value in sequence');
               end if;
            elsif l_rating_method in (
               cwms_lookup.method_previous,
               cwms_lookup.method_lower)
            then
               if l_height < l_lo_height then
                  cwms_err.raise(
                     'ERROR',
                     'PREVIOUS or LOWER specified for out of bounds low behavior');
               end if;
               l_results(i).value := l_lo_flow;
            elsif l_rating_method in (
               cwms_lookup.method_next,
               cwms_lookup.method_higher)
            then
               if l_height > l_hi_height then
                  cwms_err.raise(
                     'ERROR',
                     'NEXT or HIGHER specified for out of bounds high behavior');
               end if;
               l_results(i).value := l_hi_flow;
            elsif l_rating_method in (
               cwms_lookup.method_nearest,
               cwms_lookup.method_closest)
            then
               if l_height < l_lo_height then
                  l_results(i).value := l_lo_flow;
               elsif l_height > l_hi_height then
                  l_results(i).value := l_hi_flow;
               else
                  if l_height - l_lo_height < l_hi_height - l_height then
                     l_results(i).value := l_lo_flow;
                  else
                     l_results(i).value := l_hi_flow;
                  end if;
               end if;
            else
               cwms_err.raise('ERROR', 'Invalid rating method');
            end if;
            l_results(i).date_time := p_ind_values(i).date_time;
            l_results(i).quality_code := case l_results(i).value is null
                                          when true  then 5
                                          when false then 0
                                       end;

         end loop;
      end if;
      return l_results;
   end;

   overriding member function rate(
      p_ind_value in tsv_type)
   return tsv_type
   is
      l_results tsv_array;
   begin
      l_results := rate(tsv_array(p_ind_value));
      return l_results(1);
   end;

   overriding member function rate(
      p_ind_value in ztsv_type)
   return ztsv_type
   is
      l_results ztsv_array;
   begin
      l_results := rate(ztsv_array(p_ind_value));
      return l_results(1);
   end;

   overriding member function reverse_rate(
      p_dep_values in double_tab_t)
   return double_tab_t
   is
      l_results double_tab_t;
      l_ztsv    ztsv_array;
   begin
      if p_dep_values is not null then
         l_ztsv := ztsv_array();
         l_ztsv.extend(p_dep_values.count);
         for i in 1..p_dep_values.count loop
            l_ztsv(i).date_time := sysdate;
            l_ztsv(i).value     := p_dep_values(i);
         end loop;
         l_ztsv := reverse_rate(l_ztsv);
         l_results := double_tab_t();
         l_results.extend(p_dep_values.count);
         for i in 1..p_dep_values.count loop
            l_results(i) := l_ztsv(i).value;
         end loop;
      end if;
      return l_results;
   end;

   overriding member function reverse_rate(
      p_dep_value in binary_double)
   return binary_double
   is
      l_ztsv ztsv_type;
   begin
      l_ztsv := reverse_rate(ztsv_type(sysdate, p_dep_value, 0));
      return l_ztsv.value;
   end;

   overriding member function reverse_rate(
      p_dep_values in tsv_array)
   return tsv_array
   is
      l_results tsv_array;
      l_ztsv    ztsv_array;
      l_clone   stream_rating_t;
   begin
      if p_dep_values is not null then
         l_ztsv := ztsv_array();
         l_ztsv.extend(p_dep_values.count);
         for i in 1..p_dep_values.count loop
            l_ztsv(i).date_time    := cast(p_dep_values(i).date_time at time zone 'UTC' as date);
            l_ztsv(i).value        := p_dep_values(i).value;
            l_ztsv(i).quality_code := 0;
         end loop;
         if current_time = 'D' then
            l_ztsv := reverse_rate(l_ztsv);
         else
            l_clone := self;
            l_clone.convert_to_database_time;
            l_ztsv := l_clone.reverse_rate(l_ztsv);
         end if;
         l_results := tsv_array();
         l_results.extend(p_dep_values.count);
         for i in 1..p_dep_values.count loop
            l_results(i).date_time    := p_dep_values(i).date_time;
            l_results(i).value        := l_ztsv(i).value;
            l_results(i).quality_code := case l_results(i) is null
                                            when true  then 5
                                            when false then 0
                                         end;
         end loop;
      end if;
      return l_results;
   end;

   overriding member function reverse_rate(
      p_dep_values in ztsv_array)
   return ztsv_array
   is
      type integer_tab_t is table of pls_integer;
      c_base_date               constant date := '1900-01-01';
      l_results                 ztsv_array;
      l_date_offsets            double_tab_t;
      l_date_offset             binary_double;
      l_ratio                   binary_double;
      l_date_offsets_properties cwms_lookup.sequence_properties_t;
      l_shift                   binary_double;
      l_offset                  binary_double;
      l_heights                 double_tab_t;
      l_flows                   double_tab_t;
      l_shifts                  double_tab_t;
      l_flow                    binary_double;
      l_flows_properties        cwms_lookup.sequence_properties_t;
      i                         pls_integer;
      j                         pls_integer;
      k                         pls_integer;
      l_hi_index                pls_integer;
      l_hi_value                binary_double;
      l_lo_value                binary_double;
      l_hi_height               binary_double;
      l_lo_height               binary_double;
      l_hi_flow                 binary_double;
      l_lo_flow                 binary_double;
      l_min_height              binary_double;
      l_log_used                boolean;
      l_rating_spec             rating_spec_t;
      l_rating_method           pls_integer;
   begin
      if p_dep_values is not null then
         -------------------------
         -- get the rating spec --
         -------------------------
         l_rating_spec := rating_spec_t(rating_spec_id, office_id);
         -----------------------------------------
         -- populate the shift dates for lookup --
         -----------------------------------------
         if shifts is not null then
            l_date_offsets := double_tab_t();
            l_date_offsets.extend(shifts.count+1);
            l_date_offsets(1) := effective_date - c_base_date;
            for i in 1..shifts.count loop
               l_date_offsets(i+1) := shifts(i+1).effective_date - c_base_date;
            end loop;
            l_date_offsets_properties := cwms_lookup.analyze_sequence(l_date_offsets);
         end if;
         ------------------------------------------------------
         -- populate the rating heights and flows for lookup --
         ------------------------------------------------------
         i := 1;
         j := 1;
         k := 0;
         l_flows := double_tab_t();
         l_flows   := double_tab_t();
         -------------------------------------------------
         -- first any extension values below the rating --
         -------------------------------------------------
         if rating_info.extension_values is not null then
            while i < rating_info.extension_values.count and
                  rating_info.extension_values(i).ind_value < rating_info.rating_values(1).ind_value
            loop
               l_flows.extend;
               l_flows.extend;
               k := k + 1;
               l_flows(k) := rating_info.extension_values(i).ind_value;
               l_flows(k) := rating_info.extension_values(i).dep_value;
            end loop;
         end if;
         ----------------------------
         -- next the rating values --
         ----------------------------
         while j < rating_info.rating_values.count loop
            l_flows.extend;
            l_flows.extend;
            k := k + 1;
            l_flows(k) := rating_info.rating_values(j).ind_value;
            l_flows(k) := rating_info.rating_values(j).dep_value;
         end loop;
         ---------------------------------------------------
         -- finally any extension values above the rating --
         ---------------------------------------------------
         if rating_info.extension_values is not null then
            while i < rating_info.extension_values.count loop
               if rating_info.extension_values(i).ind_value >
                  rating_info.rating_values(rating_info.rating_values.count).ind_value
               then
                  l_flows.extend;
                  l_flows.extend;
                  k := k + 1;
                  l_flows(k) := rating_info.extension_values(i).ind_value;
                  l_flows(k) := rating_info.extension_values(i).dep_value;
               end if;
            end loop;
         end if;
         l_flows_properties := cwms_lookup.analyze_sequence(l_flows);
         -----------------------
         -- process each flow --
         -----------------------
         l_results := ztsv_array();
         l_results.extend(p_dep_values.count);
         for i in 1..p_dep_values.count loop
            l_flow := p_dep_values(i).value;
            -----------------------------------
            -- find the interpolation values --
            -----------------------------------
            l_hi_index := cwms_lookup.find_high_index(
               l_flow,
               l_flows,
               l_flows_properties);
            if l_flow < l_flows(1) then
               l_rating_method := cwms_lookup.method_by_name(l_rating_spec.out_range_low_rating_method);
            elsif l_flow > l_flows(l_flows.count) then
               l_rating_method := cwms_lookup.method_by_name(l_rating_spec.out_range_high_rating_method);
            else
               l_rating_method := cwms_lookup.method_by_name(l_rating_spec.in_range_rating_method);
            end if;
            if l_rating_method in (cwms_lookup.method_logarithmic, cwms_lookup.method_log_lin) then
               if offsets is null then
                  l_offset := 0;
               else
                  l_offset := offsets.rate(l_heights(l_hi_index-1));
               end if;
               begin
                  l_lo_height := log(10, l_heights(l_hi_index-1) - l_offset);
                  l_hi_height := log(10, l_heights(l_hi_index) - l_offset);
                  l_log_used  := true;
                  if l_rating_method = cwms_lookup.method_logarithmic then
                     if l_flow <= 0 then
                        cwms_err.raise('ERROR', 'Cannot take log of negative number');
                     end if;
                     l_lo_flow := log(10, l_flows(l_hi_index-1));
                     l_hi_flow := log(10, l_flows(l_hi_index));
                     l_flow    := log(10, l_flow);
                  end if;
               exception
                  when others then
                     l_lo_height := l_heights(l_hi_index-1);
                     l_hi_height := l_heights(l_hi_index);
                     l_lo_flow   := l_flows(l_hi_index-1);
                     l_hi_flow   := l_flows(l_hi_index);
                     l_log_used  := false;
               end;
            elsif l_rating_method = cwms_lookup.method_lin_log then
               l_lo_height := l_heights(l_hi_index-1);
               l_hi_height := l_heights(l_hi_index);
               l_log_used  := false;
               begin
                  if l_flow <= 0 then
                     cwms_err.raise('ERROR', 'Cannot take log of negative number');
                  end if;
                  l_lo_flow := log(10, l_flows(l_hi_index-1));
                  l_hi_flow := log(10, l_flows(l_hi_index));
                  l_flow    := log(10, l_flow);
               exception
                  when others then
                     l_lo_flow   := l_flows(l_hi_index-1);
                     l_hi_flow   := l_flows(l_hi_index);
               end;
            else
               l_lo_height := l_heights(l_hi_index-1);
               l_hi_height := l_heights(l_hi_index);
               l_lo_flow   := l_flows(l_hi_index-1);
               l_hi_flow   := l_flows(l_hi_index);
               l_log_used  := false;
            end if;
            -------------------------------
            -- perform the interpolation --
            -------------------------------
            if l_rating_method in (
               cwms_lookup.method_linear,
               cwms_lookup.method_logarithmic,
               cwms_lookup.method_lin_log,
               cwms_lookup.method_log_lin)
            then
               l_results(i).value :=
                  l_lo_height
                  + (l_flow - l_lo_flow)
                  / (l_hi_flow - l_lo_flow)
                  * (l_hi_height - l_lo_height);
               if l_log_used then
                  l_results(i).value := power(10, l_results(i).value) + l_offset;
               end if;
               -------------------------------------
               -- unshift the height if necessary --
               -------------------------------------
               if shifts is not null and p_dep_values(i).date_time >= effective_date then
                  l_date_offset := p_dep_values(i).date_time - c_base_date;
                  l_hi_index := cwms_lookup.find_high_index(
                     l_date_offset,
                     l_date_offsets,
                     l_date_offsets_properties);
                  l_ratio := cwms_lookup.find_ratio(
                     l_log_used,
                     l_date_offset,
                     l_date_offsets,
                     l_hi_index,
                     l_date_offsets_properties.increasing_range,
                     cwms_lookup.method_linear,
                     cwms_lookup.method_error,
                     cwms_lookup.method_nearest);
                  if l_ratio != 0. then
                     l_heights.delete;
                     l_heights.extend(shifts(l_hi_index+1).rating_info.rating_values.count);
                     l_shifts := double_tab_t();
                     l_shifts.extend(shifts(l_hi_index+1).rating_info.rating_values.count);
                     for j in 1..shifts(l_hi_index+1).rating_info.rating_values.count loop
                        l_heights(j) := shifts(l_hi_index+1).rating_info.rating_values(j).ind_value;
                        l_shifts(j) := shifts(l_hi_index+1).rating_info.rating_values(j).dep_value;
                     end loop;
                     if l_results(i).value - l_shifts(1) <= l_heights(1) then
                        l_hi_value := l_shifts(1);
                     elsif l_results(i).value - l_shifts(l_shifts.count) >= l_heights(l_heights.count) then
                        l_hi_value := l_shifts(l_shifts.count);
                     else
                        for j in 2..l_shifts.count-1 loop
                           if l_results(i).value - l_shifts(j) < l_heights(j) then
                              declare
                                 ll_lo    pls_integer;
                                 ll_hi    pls_integer;
                                 ll_ratio binary_double;
                              begin
                                 ll_lo    := j;
                                 ll_hi    := j+1;
                                 ll_ratio := (l_shifts(ll_hi)  - l_shifts(ll_lo)) /
                                             (l_heights(ll_hi) - l_heights(ll_lo));
                                 l_hi_value := l_heights(ll_lo) + (l_results(i).value - l_shifts(ll_lo))  * ll_ratio / (1. + ll_ratio);
                              end;
                              exit;
                           end if;
                        end loop;
                     end if;
                  end if;
                  if l_ratio != 1. then
                     if l_hi_index = 1 then
                        l_lo_value := 0.; -- zero shift on base curve
                     else
                        l_heights.delete;
                        l_heights.extend(shifts(l_hi_index).rating_info.rating_values.count);
                        l_shifts := double_tab_t();
                        l_shifts.extend(shifts(l_hi_index).rating_info.rating_values.count);
                        for j in 1..shifts(l_hi_index).rating_info.rating_values.count loop
                           l_heights(j) := shifts(l_hi_index).rating_info.rating_values(j).ind_value;
                           l_shifts(j) := shifts(l_hi_index).rating_info.rating_values(j).dep_value;
                        end loop;
                        if l_results(i).value - l_shifts(1) <= l_heights(1) then
                           l_lo_value := l_shifts(1);
                        elsif l_results(i).value - l_shifts(l_shifts.count) >= l_heights(l_heights.count) then
                           l_lo_value := l_shifts(l_shifts.count);
                        else
                           for j in 2..l_shifts.count-1 loop
                              if l_results(i).value - l_shifts(j) < l_heights(j) then
                                 declare
                                    ll_lo    pls_integer;
                                    ll_hi    pls_integer;
                                    ll_ratio binary_double;
                                 begin
                                    ll_lo    := j;
                                    ll_hi    := j+1;
                                    ll_ratio := (l_shifts(ll_hi)  - l_shifts(ll_lo)) /
                                                (l_heights(ll_hi) - l_heights(ll_lo));
                                    l_lo_value := l_heights(ll_lo) + (l_results(i).value - l_shifts(ll_lo))  * ll_ratio / (1. + ll_ratio);
                                 end;
                                 exit;
                              end if;
                           end loop;
                        end if;
                     end if;
                  end if;
                  if l_ratio = 0. then
                     l_shift := l_lo_value;
                  elsif l_ratio = 1. then
                     l_shift := l_hi_value;
                  else
                     l_shift := l_lo_value + l_ratio * (l_hi_value - l_lo_value);
                  end if;
                  l_results(i).value := l_results(i).value - l_shift;
               end if;
            elsif l_rating_method = cwms_lookup.method_null then
               l_results(i).value := null;
            elsif l_rating_method = cwms_lookup.method_error then
               if l_flow < l_lo_flow then
                  cwms_err.raise(
                     'ERROR',
                     'Value is out of bounds low');
               elsif l_flow > l_hi_flow then
                  cwms_err.raise(
                     'ERROR',
                     'Value is out of bounds high');
               else
                  cwms_err.raise(
                     'ERROR',
                     'Value does not match any value in sequence');
               end if;
            elsif l_rating_method in (
               cwms_lookup.method_previous,
               cwms_lookup.method_lower)
            then
               if l_flow < l_lo_flow then
                  cwms_err.raise(
                     'ERROR',
                     'PREVIOUS or LOWER specified for out of bounds low behavior');
               end if;
               l_results(i).value := l_lo_height;
            elsif l_rating_method in (
               cwms_lookup.method_next,
               cwms_lookup.method_higher)
            then
               if l_flow > l_hi_flow then
                  cwms_err.raise(
                     'ERROR',
                     'NEXT or HIGHER specified for out of bounds high behavior');
               end if;
               l_results(i).value := l_hi_height;
            elsif l_rating_method in (
               cwms_lookup.method_nearest,
               cwms_lookup.method_closest)
            then
               if l_flow < l_lo_flow then
                  l_results(i).value := l_lo_height;
               elsif l_flow > l_hi_flow then
                  l_results(i).value := l_hi_height;
               else
                  if l_flow - l_lo_flow < l_hi_flow - l_flow then
                     l_results(i).value := l_lo_height;
                  else
                     l_results(i).value := l_hi_height;
                  end if;
               end if;
            else
               cwms_err.raise('ERROR', 'Invalid rating method');
            end if;
            l_results(i).date_time := p_dep_values(i).date_time;
            l_results(i).quality_code := case l_results(i).value is null
                                          when true  then 5
                                          when false then 0
                                       end;

         end loop;
      end if;
      return l_results;
   end;

   overriding member function reverse_rate(
      p_dep_value in tsv_type)
   return tsv_type
   is
      l_results tsv_array;
   begin
      l_results := reverse_rate(tsv_array(p_dep_value));
      return l_results(1);
   end;

   overriding member function reverse_rate(
      p_dep_value in ztsv_type)
   return ztsv_type
   is
      l_results ztsv_array;
   begin
      l_results := reverse_rate(ztsv_array(p_dep_value));
      return l_results(1);
   end;

   member procedure trim_to_effective_date(
      p_date_time in date)
   is
   begin
      if shifts is not null then
         for i in reverse 1..shifts.count loop
            exit when shifts(i).effective_date < p_date_time;
            shifts.trim(1);
         end loop;
         if shifts.count = 0 then
            shifts := null;
         end if;
      end if;
   end;

   member procedure trim_to_create_date(
      p_date_time in date)
   is
      l_count  pls_integer := 0;
      l_shifts rating_tab_t;
   begin
      if shifts is not null then
         for i in 1..shifts.count loop
            if shifts(i).create_date > p_date_time then
               l_count := l_count + 1;
            end if;
         end loop;
         if l_count > 0 then
            if l_count < shifts.count then
               l_shifts := rating_tab_t();
               for i in 1..shifts.count loop
                  if shifts(i).create_date <= p_date_time then
                     l_shifts.extend;
                     l_shifts(l_shifts.count) := shifts(i);
                  end if;
               end loop;
            end if;
            shifts := l_shifts;
         end if;
      end if;
   end;

   member function latest_shift_date
   return date
   is
   begin
      return case shifts is null
                when true  then null
                when false then shifts(shifts.count).effective_date
             end;
   end;
end;
 
  GRANT EXECUTE ON "CWMS_20"."STREAM_RATING_T" TO "CWMS_USER"
 