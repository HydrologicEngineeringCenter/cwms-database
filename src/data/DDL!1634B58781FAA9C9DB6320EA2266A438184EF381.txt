
  CREATE OR REPLACE PACKAGE "CWMS_20"."CWMS_LOCK" 
/**
 * Facilities for working with locks at CWMS project
 *
 * @author Peter Morris
 *
 * @since CWMS 2.1
 */
AS
/**
 * Catalogs locks stored in the database that for a specified CWMS project.
 *
 * @param p_lock_cat A cursor containing all the locks for the specified project
 * <p>
 * <table class="descr">
 *   <tr>
 *     <th class="descr">Column No.</th>
 *     <th class="descr">Column Name</th>
 *     <th class="descr">Data Type</th>
 *     <th class="descr">Contents</th>
 *   </tr>
 *   <tr>
 *     <td class="descr-center">1</td>
 *     <td class="descr">project_office_id</td>
 *     <td class="descr">varchar2(16)</td>
 *     <td class="descr">The office that owns the project location</td>
 *   </tr>
 *   <tr>
 *     <td class="descr-center">2</td>
 *     <td class="descr">project_id</td>
 *     <td class="descr">varchar2(49)</td>
 *     <td class="descr">The location identifier of project</td>
 *   </tr>
 *   <tr>
 *     <td class="descr-center">3</td>
 *     <td class="descr">db_office_id</td>
 *     <td class="descr">varchar2(16)</td>
 *     <td class="descr">The office that owns the lock location</td>
 *   </tr>
 *   <tr>
 *     <td class="descr-center">4</td>
 *     <td class="descr">base_location_id</td>
 *     <td class="descr">varchar2(16)</td>
 *     <td class="descr">The base location identifier of the lock</td>
 *   </tr>
 *   <tr>
 *     <td class="descr-center">5</td>
 *     <td class="descr">sub_location_id</td>
 *     <td class="descr">varchar2(32)</td>
 *     <td class="descr">The sub-location identifier, if any, of the lock</td>
 *   </tr>
 *   <tr>
 *     <td class="descr-center">6</td>
 *     <td class="descr">time_zone_name</td>
 *     <td class="descr">varchar2(28)</td>
 *     <td class="descr">The local time zone for the lock location</td>
 *   </tr>
 *   <tr>
 *     <td class="descr-center">7</td>
 *     <td class="descr">latitude</td>
 *     <td class="descr">number</td>
 *     <td class="descr">The actual latitude of the lock location</td>
 *   </tr>
 *   <tr>
 *     <td class="descr-center">8</td>
 *     <td class="descr">longitude</td>
 *     <td class="descr">number</td>
 *     <td class="descr">The actual longitude of the lock location</td>
 *   </tr>
 *   <tr>
 *     <td class="descr-center">9</td>
 *     <td class="descr">horizontal_datum</td>
 *     <td class="descr">varchar2(16)</td>
 *     <td class="descr">The datum of the latitude and longitude</td>
 *   </tr>
 *   <tr>
 *     <td class="descr-center">10</td>
 *     <td class="descr">elevation</td>
 *     <td class="descr">number</td>
 *     <td class="descr">The elevation of the lock location</td>
 *   </tr>
 *   <tr>
 *     <td class="descr-center">11</td>
 *     <td class="descr">elev_unit_id</td>
 *     <td class="descr">varchar2(16)</td>
 *     <td class="descr">The elvation unit</td>
 *   </tr>
 *   <tr>
 *     <td class="descr-center">12</td>
 *     <td class="descr">vertical_datum</td>
 *     <td class="descr">varchar2(16)</td>
 *     <td class="descr">The datum of the elevation</td>
 *   </tr>
 *   <tr>
 *     <td class="descr-center">13</td>
 *     <td class="descr">public_name</td>
 *     <td class="descr">varchar2(32)</td>
 *     <td class="descr">The public name of the lock location</td>
 *   </tr>
 *   <tr>
 *     <td class="descr-center">14</td>
 *     <td class="descr">long_name</td>
 *     <td class="descr">varchar2(80)</td>
 *     <td class="descr">The long name of the lock location</td>
 *   </tr>
 *   <tr>
 *     <td class="descr-center">15</td>
 *     <td class="descr">description</td>
 *     <td class="descr">varchar2(512)</td>
 *     <td class="descr">A description of the lock location</td>
 *   </tr>
 *   <tr>
 *     <td class="descr-center">16</td>
 *     <td class="descr">active_flag</td>
 *     <td class="descr">varchar2(1)</td>
 *     <td class="descr">Specifies whether the lock location is active</td>
 *   </tr>
 * </table>
 *
 * @param p_project_id  The location identifier of the CWMS project
 *
 * @param p_db_office_id  The office pattern to match.  If the routine is called
 * without this parameter, or if this parameter is set to NULL, the session user's
 * default office will be used.
 */
PROCEDURE cat_lock (
   p_lock_cat     OUT sys_refcursor,
   p_project_id   IN    VARCHAR2 DEFAULT NULL,
   p_db_office_id IN    VARCHAR2 DEFAULT NULL);
/**
 * Retrieves info for a specified lock from the database
 *
 * @param p_lock The retrieved lock info
 * @param p_lock_location_ref Identifies the lock to retrieve
 */
PROCEDURE retrieve_lock(
   p_lock OUT lock_obj_t,
   p_lock_location_ref IN location_ref_t);
/**
 * Stores a lock to the database
 *
 * @param p_lock The lock to store (insert or update)
 * @param p_fail_if_exists A flag ('T' or 'F') specifying whether the routine should fail if the specified lock already exists in the database
 *
 * @exception ITEM_ALREADY_EXISTS if p_fail_if_exists is 'T' and the specified lock already exists in the database
 */
procedure store_lock(
   p_lock           IN lock_obj_t,
   p_fail_if_exists IN VARCHAR2 DEFAULT 'T');
/**
 * Renames an lock in the database
 *
 * @param p_lock_id_old The existing location identifier of the lock
 * @param p_lock_id_new The new location identifier of the lock
 * @param p_db_office_id      The office that owns the lock.  If not specified or NULL, the session user's default office will be used.
 */
procedure rename_lock(
   p_lock_id_old  IN VARCHAR2,
   p_lock_id_new  IN VARCHAR2,
   p_db_office_id IN VARCHAR2 DEFAULT NULL);
/**
 * Deletes an lock from the database
 *
 * @see constant cwms_util.delete_key
 * @see constant cwms_util.delete_data
 * @see constant cwms_util.delete_all
 *
 * @param p_lock_id_ The location identifier of the lock to delete
 * @param p_delete_action  Specifies what to delete. Actions are as follows:
 * <p>
 * <table class="descr">
 *   <tr>
 *     <th class="descr">p_delete_action</th>
 *     <th class="descr">Action</th>
 *   </tr>
 *   <tr>
 *     <td class="descr">cwms_util.delete_key</td>
 *     <td class="descr">deletes only the lock location, and then only if it has no data referencing it</td>
 *   </tr>
 *   <tr>
 *     <td class="descr">cwms_util.delete_data</td>
 *     <td class="descr">deletes data that references this lock location, if any</td>
 *   </tr>
 *   <tr>
 *     <td class="descr">cwms_util.delete_all</td>
 *     <td class="descr">deletes this lock location and all data referencing it</td>
 *   </tr>
 * </table>
 * @param p_db_office_id   The office that owns the lock.  If not specified or NULL, the session user's default office will be used.
 */
procedure delete_lock(
    p_lock_id       IN VARCHAR,
    p_delete_action IN VARCHAR2 DEFAULT cwms_util.delete_key,
    p_db_office_id  IN VARCHAR2 DEFAULT NULL);

END CWMS_LOCK;
CREATE OR REPLACE PACKAGE BODY "CWMS_20"."CWMS_LOCK" AS

-------------------------------------------------------------------------------
-- CWMS_LOCK
--
-- These procedures and functions query and manipulate locks in the CWMS/ROWCPS
-- database.


--
-- cat_project
--
-- security: can be called by user and dba group.
--
-- NOTE THAT THE COLUMN NAMES SHOULD NOT BE CHANGED AFTER BEING DEVELOPED.
-- Changing them will end up breaking external code (so make any changes prior
-- to development).
-- The returned records contain the following columns:
--
--    Name                      Datatype      Description
--    ------------------------ ------------- ----------------------------
--    db_office_id             varchar2(16)   owning office of location
--    project_location_id      varchar2(49)   the parent project's location id
--    base_location_id         varchar2(16)   base location id
--    sub_location_id          varchar2(32)   sub-location id, if any
--    time_zone_name           varchar2(28)   local time zone name for location
--    latitude                 number         location latitude
--    longitude                number         location longitude
--    horizontal_datum         varchar2(16)   horizontal datrum of lat/lon
--    elevation                number         location elevation
--    elev_unit_id             varchar2(16)   location elevation units
--    vertical_datum           varchar2(16)   veritcal datum of elevation
--    public_name              varchar2(32)   location public name
--    long_name                varchar2(80)   location long name
--    description              varchar2(512)  location description
--    active_flag              varchar2(1)    'T' if active, else 'F'
--
-------------------------------------------------------------------------------
--type definitions:
--
--
-------------------------------------------------------------------------------
--
-------------------------------------------------------------------------------
-- errors will be issued as thrown exceptions.
--
PROCEDURE cat_lock (
   p_lock_cat     OUT sys_refcursor,         --described above.
   p_project_id   IN  VARCHAR2 DEFAULT NULL, -- the project id. if null, return all locks for the office.
   p_db_office_id IN  VARCHAR2 DEFAULT NULL) -- defaults to the connected user's office if null
                                             -- the office id can use sql masks for retrieval of additional offices.
is
   l_office_id_mask varchar2(16) := cwms_util.normalize_wildcards(nvl(upper(p_db_office_id), cwms_util.user_office_id), true);
begin
   open p_lock_cat for
      select po.office_id as project_office_id,
             cwms_util.concat_base_sub_id(
               pbl.base_location_id,
               ppl.sub_location_id) as project_location_id,  --    project_location_id      varchar2(49)   the parent project's location id
             o.office_id as db_office_id,                    --    db_office_id             varchar2(16)   owning office of location
             bl.base_location_id,                           --    base_location_id         varchar2(16)   base location id
             pl.sub_location_id,                            --    sub_location_id          varchar2(32)   sub-location id, if any
             tz.time_zone_name,                              --    time_zone_name           varchar2(28)   local time zone name for location
             pl.latitude,                                   --    latitude                 number         location latitude
             pl.longitude,                                  --    longitude                number         location longitude
             pl.horizontal_datum,                           --    horizontal_datum         varchar2(16)   horizontal datrum of lat/lon
             pl.elevation,                                  --    elevation                number         location elevation
             u.unit_id as elevation_unit_id,                 --    elev_unit_id             varchar2(16)   location elevation units
             pl.vertical_datum,                             --    vertical_datum           varchar2(16)   veritcal datum of elevation
             pl.public_name,                                --    public_name              varchar2(32)   location public name
             pl.long_name,                                  --    long_name                varchar2(80)   location long name
             pl.description,                                --    description              varchar2(512)  location description
             pl.active_flag                                 --    active_flag              varchar2(1)    'T' if active, else 'F'
        from cwms_office o,
             cwms_office po,
             at_project p,
             at_lock l,
             at_physical_location ppl, -- project location
             at_base_location pbl,     -- project location
             at_physical_location pl, -- lock location
             at_base_location bl,     -- lock location
             cwms_time_zone tz,
             cwms_base_parameter bp,
             cwms_unit u
       where o.office_id like l_office_id_mask
         and po.office_code = pbl.db_office_code
         and o.office_code = bl.db_office_code
         and ppl.base_location_code = pbl.base_location_code
         and cwms_util.concat_base_sub_id(pbl.base_location_id, ppl.sub_location_id)
             = nvl(p_project_id, cwms_util.concat_base_sub_id(pbl.base_location_id, ppl.sub_location_id))
         and p.project_location_code = ppl.location_code
         and l.project_location_code = p.project_location_code
         and pl.location_code = l.lock_location_code
         and bl.base_location_code = pl.base_location_code
         --and tz.time_zone_code = pl.time_zone_code
         and tz.time_zone_code = nvl(
                  pl.time_zone_code,
                  (  select time_zone_code
                       from cwms_time_zone
                      where time_zone_name = 'UTC'
                  ))
         and bp.base_parameter_id = 'Elev'
         and u.unit_code = bp.unit_code;

end cat_lock;


PROCEDURE retrieve_lock_old(
   p_lock OUT lock_obj_t,                  --returns a filled in lock object including location data
   p_lock_location_ref IN location_ref_t)  -- a location ref that identifies the lock we want to retrieve.
                                           -- includes the lock's location id (base location + '-' + sublocation)
                                           -- the office id if null will default to the connected user's office
is
   l_lock_rec      at_lock%rowtype;
   l_lock_loc      location_obj_t;
   l_lock_loc_rec  at_physical_location%rowtype;
   l_lock_location_ref location_ref_t;
   l_cwms_office_code number;
begin
  -- get the cwms office code.
  l_cwms_office_code := cwms_util.get_office_code('CWMS');

   --------------------------------------------------------------
   -- set up a location ref that defaults to the user's office --
   --------------------------------------------------------------
   l_lock_location_ref := location_ref_t(
      p_lock_location_ref.get_location_id,p_lock_location_ref.office_id);
   if l_lock_location_ref.office_id is null then
      l_lock_location_ref.office_id := cwms_util.user_office_id;
   end if;
   --------------------------------------------------------------
   -- select rows from at_lock and at_physical location tables --
   --------------------------------------------------------------
   begin
      select *
        into l_lock_rec
        from at_lock
       where lock_location_code = l_lock_location_ref.get_location_code;
   exception
      when no_data_found then
         cwms_err.raise(
            'ITEM_DOES_NOT_EXIST',
            'Lock location',
            l_lock_location_ref.office_id||'/'||l_lock_location_ref.get_location_id);
   end;
   begin
      select *
        into l_lock_loc_rec
        from at_physical_location
       where location_code = l_lock_location_ref.get_location_code;
   exception
      when no_data_found then
         cwms_err.raise(
            'ITEM_DOES_NOT_EXIST',
            'Lock location',
            l_lock_location_ref.office_id||'/'||l_lock_location_ref.get_location_id);
   end;
   -------------------------------------
   -- create the lock location object --
   -------------------------------------
   l_lock_loc := location_obj_t(
      l_lock_location_ref,
      null, -- state_initial
      null, -- county_name
      null, -- time_zone_name
      l_lock_loc_rec.location_type,
      l_lock_loc_rec.latitude,
      l_lock_loc_rec.longitude,
      l_lock_loc_rec.horizontal_datum,
      l_lock_loc_rec.elevation,
      null, -- elev_unit_id
      l_lock_loc_rec.vertical_datum,
      l_lock_loc_rec.public_name,
      l_lock_loc_rec.long_name,
      l_lock_loc_rec.description,
      l_lock_loc_rec.active_flag,
      null, -- l_location_kind_id
      l_lock_loc_rec.map_label,
      l_lock_loc_rec.published_latitude,
      l_lock_loc_rec.published_longitude,
      null, -- l_bounding_office_id
      null, -- l_bounding_office_name
      null, -- l_nation_id
      l_lock_loc_rec.nearest_city);
   ------------------------------------------------------------------------------------
   -- complete the lock location object from codes in the at_physical location table --
   ------------------------------------------------------------------------------------
   begin
      select s.state_initial,
             c.county_name,
             tz.time_zone_name,
             u.unit_id,
             lk.location_kind_id,
             o.office_id,
             o.public_name,
             n.nation_id
        into l_lock_loc.state_initial,
             l_lock_loc.county_name,
             l_lock_loc.time_zone_name,
             l_lock_loc.elev_unit_id,
             l_lock_loc.location_kind_id,
             l_lock_loc.bounding_office_id,
             l_lock_loc.bounding_office_name,
             l_lock_loc.nation_id
        from cwms_county c,
             cwms_state s,
             cwms_time_zone tz,
             cwms_base_parameter bp,
             cwms_unit u,
             at_location_kind lk,
             at_base_location bl,
             cwms_office o,
             cwms_nation n
       where c.county_code = nvl(l_lock_loc_rec.county_code, 0)
         and s.state_code = c.state_code
         and tz.time_zone_code = nvl(l_lock_loc_rec.time_zone_code, 0)
         and bp.base_parameter_id = 'Elev'
         and u.unit_code = bp.unit_code
         and bl.base_location_code = l_lock_loc_rec.base_location_code
         and (lk.office_code = bl.db_office_code or lk.office_code = l_cwms_office_code)
         and lk.location_kind_code = nvl(l_lock_loc_rec.location_kind, 1)
         and o.office_code = nvl(l_lock_loc_rec.office_code, 0)
         -- and o.office_code = l_lock_loc_rec.office_code
         and n.nation_code = nvl(l_lock_loc_rec.nation_code, 'US');
   exception
      when no_data_found then
         cwms_err.raise(
            'ERROR',
            'The following dataset could not be found:'
            ||chr(10)||chr(9)||'cwms_count.county_code                = '||nvl(l_lock_loc_rec.county_code, -1)
            ||chr(10)||chr(9)||'cwms_state.state_code                 = cwms_count.state_code'
            ||chr(10)||chr(9)||'cwms_timee_zone.time_zone_code        = '||nvl(l_lock_loc_rec.time_zone_code, -1)
            ||chr(10)||chr(9)||'cwms_base_parameter.base_parameter_id = ''Elev'''
            ||chr(10)||chr(9)||'cwms_unit.unit_code                   = cwms_base_parameter.unit_code'
            ||chr(10)||chr(9)||'at_base_location.location_code        = '||l_lock_loc_rec.base_location_code
            ||chr(10)||chr(9)||'at_location_kind.office_code          = at_base_location.db_office_code'
            ||chr(10)||chr(9)||'at_location_kind.location_kind_code   = '||nvl(l_lock_loc_rec.location_kind, -1)
            ||chr(10)||chr(9)||'cwms_office.office_code               = '||nvl(l_lock_loc_rec.office_code, -1)
            ||chr(10)||chr(9)||'cwms_nation.nation_code               = '||nvl(l_lock_loc_rec.nation_code, 'XX'));
   end;
   ----------------------------
   -- create the lock object --
   ----------------------------
   p_lock := lock_obj_t(
      location_ref_t(l_lock_rec.project_location_code),
      l_lock_loc,
      l_lock_rec.volume_per_lockage,
      null, -- volume units.
      l_lock_rec.lock_width,
      l_lock_rec.lock_length,
      l_lock_rec.minimum_draft,
      l_lock_rec.normal_lock_lift,
      null --  length units.
    );

end retrieve_lock_old;

PROCEDURE retrieve_lock(
   p_lock OUT lock_obj_t,                  --returns a filled in lock object including location data
   p_lock_location_ref IN location_ref_t)  -- a location ref that identifies the lock we want to retrieve.
                                           -- includes the lock's location id (base location + '-' + sublocation)
                                           -- the office id if null will default to the connected user's office
is
   l_lock_loc location_obj_t;
   l_unit                varchar2(16);
   l_factor              number;
begin
   p_lock := null;

  ----------------------------------------------------------------------------------
   -- use the cursor loop construct for convenience, there will only be one record --
   ----------------------------------------------------------------------------------
   for rec in
      (	select l.lock_location_code,
                l.project_location_code,
                l.volume_per_lockage,
                l.lock_width,
                l.lock_length,
                l.minimum_draft,
                l.normal_lock_lift
           from at_lock l
          where l.lock_location_code = p_lock_location_ref.get_location_code)
   loop
   ----------------------------
   -- create the lock object --
   ----------------------------
   p_lock := lock_obj_t(
      location_ref_t(rec.project_location_code),
      cwms_loc.retrieve_location(rec.lock_location_code),
      rec.volume_per_lockage,
      cwms_util.get_default_units('Volume'), -- volume units.
      rec.lock_width,
      rec.lock_length,
      rec.minimum_draft,
      rec.normal_lock_lift,
      cwms_util.get_default_units('Length') --  length units.
    );
   end loop;
end retrieve_lock;

procedure store_lock(
   p_lock           IN lock_obj_t,           -- a populated lock object type.
   p_fail_if_exists IN VARCHAR2 DEFAULT 'T') -- a flag that will cause the procedure to fail if the lock already exists
is
   l_lock_rec      at_lock%rowtype;
   l_exists        boolean;
   l_length_factor binary_double;
   l_length_offset binary_double;
   l_volume_factor binary_double;
   l_volume_offset binary_double;
begin
   -------------------------------------
   -- retrieve the lock, if it exists --
   -------------------------------------
   begin
      select *
        into l_lock_rec
        from at_lock
       where lock_location_code = p_lock.lock_location.location_ref.get_location_code;
      l_exists := true;
   exception
      when no_data_found then
         l_exists := false;
   end;
   ----------------------------
   -- error out if necessary --
   ----------------------------
   if l_exists and cwms_util.return_true_or_false(p_fail_if_exists) then
      cwms_err.raise(
         'ITEM_ALREADY_EXISTS',
         'Lock',
         p_lock.lock_location.location_ref.office_id||'/'||p_lock.lock_location.location_ref.get_location_id);
   end if;
   ------------------------------------------------------
   -- verify that the project location already exists, --
   -- we don't have enough info to create it           --
   ------------------------------------------------------
   begin
      l_lock_rec.project_location_code := p_lock.project_location_ref.get_location_code;
   exception
      when no_data_found then
         cwms_err.raise(
            'ITEM_DOES_NOT_EXIST',
            'Project for lock location',
            p_lock.project_location_ref.office_id||'/'||p_lock.project_location_ref.get_location_id);
   end;
   --------------------------------
   -- get the conversion factors --
   --------------------------------
   select factor,
          offset
     into l_length_factor,
          l_length_offset
     from cwms_unit_conversion uc,
          cwms_base_parameter bp
    where uc.from_unit_id = p_lock.units_id
      and bp.base_parameter_id = 'Length'
      and uc.to_unit_code = bp.unit_code;

   select factor,
          offset
     into l_volume_factor,
          l_volume_offset
     from cwms_unit_conversion uc,
          cwms_base_parameter bp
    where uc.from_unit_id = p_lock.volume_units_id
      and bp.base_parameter_id = 'Volume'
      and uc.to_unit_code = bp.unit_code;

   ----------------------------------------------------------
   -- fill out the lock record, don't overwrite with nulls --
   ----------------------------------------------------------
   l_lock_rec.lock_width :=
      case p_lock is null
         when true  then l_lock_rec.lock_width
         when false then p_lock.lock_width * l_length_factor + l_length_offset
      end;
   l_lock_rec.lock_length :=
      case p_lock is null
         when true  then l_lock_rec.lock_length
         when false then p_lock.lock_length * l_length_factor + l_length_offset
      end;
   l_lock_rec.volume_per_lockage :=
      case p_lock is null
         when true  then l_lock_rec.volume_per_lockage
         when false then p_lock.volume_per_lockage * l_volume_factor + l_volume_offset
      end;
   l_lock_rec.minimum_draft :=
      case p_lock is null
         when true  then l_lock_rec.minimum_draft
         when false then p_lock.minimum_draft * l_length_factor + l_length_offset
      end;
   l_lock_rec.normal_lock_lift :=
      case p_lock is null
         when true  then l_lock_rec.normal_lock_lift
         when false then p_lock.normal_lock_lift * l_length_factor + l_length_offset
      end;
   ---------------------------------
   -- insert or update the record --
   ---------------------------------
   if l_exists then
      -------------
      -- update ---
      -------------
      cwms_loc.store_location(p_lock.lock_location,'F');
      update at_lock
         set row = l_lock_rec
       where lock_location_code = l_lock_rec.lock_location_code;
   else
      ------------
      -- insert --
      ------------
      begin
         l_lock_rec.lock_location_code := p_lock.lock_location.location_ref.get_location_code;
      exception
         when no_data_found then
            --------------------------------------
            -- need to create the lock location --
            --------------------------------------
            cwms_loc.store_location(p_lock.lock_location,'F');
            l_lock_rec.lock_location_code := p_lock.lock_location.location_ref.get_location_code;
      end;
      insert
        into at_lock
      values l_lock_rec;
   end if;
end store_lock;


procedure rename_lock(
   p_lock_id_old  IN VARCHAR2,              -- the old lock concatenated location id
   p_lock_id_new  IN VARCHAR2,              -- the new lock concatenated location id
   p_db_office_id IN VARCHAR2 DEFAULT NULL) -- defaults to the connected user's office if null
is
begin
   cwms_loc.rename_location(p_lock_id_old, p_lock_id_new, p_db_office_id);
end rename_lock;


procedure delete_lock(
    p_lock_id       IN VARCHAR,                               -- base location id + "-" + sub-loc id (if it exists)
    p_delete_action IN VARCHAR2 DEFAULT cwms_util.delete_key, --
    p_db_office_id  IN VARCHAR2 DEFAULT NULL)                 -- defaults to the connected user's office if null
is
   l_child_loc_code NUMBER;
begin

  cwms_util.check_inputs(str_tab_t(p_lock_id, p_delete_action,p_db_office_id));
  IF NOT p_delete_action IN (cwms_util.delete_key, cwms_util.delete_all ) THEN
    cwms_err.raise(
       'ERROR',
       'P_DELETE_ACTION must be '''
       || cwms_util.delete_key
       || ''' or '''
       || cwms_util.delete_all
       || '');
  END IF;

  l_child_loc_code := cwms_loc.get_location_code(p_db_office_id,p_lock_id);

  IF p_delete_action = cwms_util.delete_all THEN
      -- delete settings
      DELETE
        FROM at_lockage
       WHERE lockage_location_code = l_child_loc_code;

   END IF; -- delete all

   -- delete from at_lock
   DELETE
     FROM at_lock
    WHERE lock_location_code = l_child_loc_code;


end delete_lock;

END CWMS_LOCK;


 
  GRANT EXECUTE ON "CWMS_20"."CWMS_LOCK" TO "CWMS_USER"
 