plugins {
    id 'java-library'
    id 'maven-publish'
    id 'jacoco'
    id 'org.sonarqube' version '2.7'
}



group = "mil.army.usace.hec"
version = "2.0.0-SNAPSHOT"

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {

    implementation "org.testcontainers:testcontainers:1.16.0"
    implementation "org.testcontainers:database-commons:1.16.0"
    implementation "org.testcontainers:jdbc:1.16.0"


    testImplementation "org.testcontainers:junit-jupiter:1.16.0"
    testImplementation 'com.oracle.ojdbc:ojdbc8:19.3.0.0'
    testImplementation 'org.mockito:mockito-core:3.4.6'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.4.6'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.6.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.6.2'

    testImplementation 'ch.qos.logback:logback-classic:1.2.7'

}


test {
    useJUnitPlatform()
    jvmArgs += "-Djava.net.preferIPv4Stack=true"
    jvmArgs += "-Dlogback.configurationFile=$projectDir/logback-config.xml"
    systemProperties += System.getProperties().findAll{ k,v -> k.startsWith("teamcity") || k.startsWith("cwms") }
    systemProperties += project.properties.findAll{ k,v -> k.startsWith("teamcity") || k.startsWith("cwms") }
}

publishing {
		repositories {
			maven {
				def baseUrl = 'https://www.hec.usace.army.mil/nexus/repository'
				def releasesRepoUrl = "$baseUrl/maven-releases"
				def snapshotsRepoUrl = "$baseUrl/maven-snapshots"
				url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
				credentials {
					username "$mavenUser"
					password "$mavenPassword"
				}
			}
		}

        publications {
        mavenJava(MavenPublication) {
            artifactId = 'testcontainers-cwms'
            from components.java
        }
    }
}
