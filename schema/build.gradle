buildscript {
    dependencies {
        classpath "com.oracle.database.jdbc:ojdbc11:$ORACLE_JDBC_VERSION"
        classpath "org.flywaydb:flyway-database-oracle:10.0.0"
    }

}

plugins {
    id 'java'
    id 'org.flywaydb.flyway' version '10.0.0'
}

//defaults
var build_user = project.findProperty("cwms.user") ?: 'must set user to run these tasks'
def cwms_schema_name = project.findProperty("cwms.schema") ?: "CWMS_20"
def cwms_office = project.findProperty("cwms.office") ?:"SPK"
def cwms_eroc = project.findProperty("cwms.eroc") ?:"l2"
def create_users = project.hasProperty("testaccounts") ? "create" : "nocreate"
def pd_password = project.findProperty("cwms.pd_password") ?: project.findProperty("flyway.password")
def test_password = project.findProperty("cwms.test_password") ?: pd_password
def coverageDir = "$buildDir/coverage"
def cwms_user = "${build_user}[$cwms_schema_name]"

configurations {
    compile
    flywayMigration
    utplsql
}

dependencies {
    implementation "org.flywaydb:flyway-core:10.0.0"
    implementation "net.objecthunter:exp4j:0.4.8"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.15.2"
    implementation 'commons-io:commons-io:2.11.0'
    //implementation 'java-algebra-system:JMCAS:3.0'
    implementation 'org.opendcs:java-algebra-system:0.0.1'
    compile "org.flywaydb:flyway-core:10.0.0"
    flywayMigration "com.oracle.database.jdbc:ojdbc11:$ORACLE_JDBC_VERSION"
    flywayMigration "org.flywaydb:flyway-database-oracle:10.0.0"
    flywayMigration sourceSets.main.runtimeClasspath
    utplsql 'org.utplsql:utplsql:3.1.10@zip'
}

flyway {
    locations = ["db/schema"]
    configurations = ['flywayMigration','compile','runtimeClasspath']
    user = build_user
    mixed = true
    schemas = [cwms_schema_name,'CWMS_DBA']
    placeholders = [
        "CWMS_SCHEMA": cwms_schema_name,
        "CWMS_SCHEMA_AUTH": "no authentication", // override on commandline with identified by portion if needed.
        "CWMS_DBA_AUTH": "no authentication",
        "CWMS_TABLESPACE": "CWMS_20DATA",
        "AT_TABLESPACE": "CWMS_20ATDATA",
        "AQ_TABLESPACE": "CWMS_AQ",
        "AQ_EX_TABLESPACE": "CWMS_AQ_EX",
        "CWMS_OFFICE_ID": cwms_office,
        "CWMS_OFFICE_EROC": cwms_eroc,
        "CWMS_TEST_USERS": create_users,
        "PD_PASSWORD": pd_password,
        "TEST_PASSWORD": test_password
    ]
}

task dataMigrate(type: org.flywaydb.gradle.task.FlywayMigrateTask){ /*  dependsOn: flywayMigrate){*/
    locations = ["db/data"]
    configurations = ['flywayMigration','compile','runtimeClasspath']
    user = cwms_user
    url = flyway.url
    password = flyway.password
    mixed = true
    resolvers =  ['cwms.resolvers.CwmsBulkDataResolver']
    table = "flyway_data_history"
    baselineOnMigrate = true // The structure migration task has already created a table here. We need a 2nd one.
    schemas = [cwms_schema_name]
    placeholders = [
        "CWMS_SCHEMA": cwms_schema_name,
        "CWMS_OFFICE_ID": cwms_office,
        "CWMS_OFFICE_EROC": cwms_eroc
    ]
}

task dataRepair(type: org.flywaydb.gradle.task.FlywayRepairTask){
    locations = ["db/data"]
    configurations = ['flywayMigration','compile','runtimeClasspath']
    user = "${build_user}[$cwms_schema_name]"
    url = flyway.url
    password = flyway.password
    mixed = true
    table = "flyway_data_history"
    baselineOnMigrate = true // The structure migration task has already created a table here. We need a 2nd one.
    schemas = [cwms_schema_name]
    placeholders = [
        "CWMS_SCHEMA": cwms_schema_name,
        "CWMS_OFFICE_ID": cwms_office,
        "CWMS_OFFICE_EROC": cwms_eroc
    ]
}

flywayMigrate.dependsOn classes
flywayClean.dependsOn classes
flywayRepair.dependsOn classes
dataMigrate.dependsOn classes
dataMigrate.dependsOn flywayMigrate
dataRepair.dependsOn classes

var sysPassword = project.findProperty("db.sys_password")
def tmp = project.findProperty("flyway.url")
def dbUrl = tmp == null ? tmp: tmp.split("@")[1]

task checkTestSetup {
    doFirst {
        def result = exec {
            executable "sqlplus"
            args "-h"
            ignoreExitValue = true
            standardOutput = new ByteArrayOutputStream()
            errorOutput = new ByteArrayOutputStream()
        }

        if( result.getExitValue() !=0){
            throw new GradleException("sqlplus must be available for test framework to execute")
        }

        result = exec {
            executable "utplsql"
            args "-h"
            ignoreExitValue = true
            standardOutput = new ByteArrayOutputStream()
            errorOutput = new ByteArrayOutputStream()
        }

        if( result.getExitValue() != 0 ) {
            throw new GradleException("utplsql-cli must be installed and in PATH for tests to run")
        }


        if( sysPassword == null) {
            throw new GradleException("Tests can only be run if db.sys_password is set.")
        }

        if( dbUrl == null ){
            throw new GradleException("Test framework assumes you are allow running flyway Migrate tasks, please set flyway.url")
        }
    }
}

task downloadUtplSql(type: Copy, dependsOn: checkTestSetup ){

    from zipTree(configurations.utplsql.singleFile)
    into "$buildDir/deps"
    doLast {
        File createOwner = new File("$buildDir/deps/utPLSQL/source/create_utplsql_owner.sql")
        createOwner.withWriterAppend( { out ->
            out.println("\ngrant execute on dbms_lob to &ut3_user;")
            out.println("grant execute on dbms_random to &ut3_user;")
	  		out.println("grant execute on dbms_random to &ut3_user;")
	  		out.println("grant execute on dbms_obfuscation_toolkit to &ut3_user;")
	  		out.println("grant execute on dbms_sql to &ut3_user;")
        })
    }
}

task installTestFramework(type: Exec ) {
    dependsOn downloadUtplSql
    dependsOn checkTestSetup

    executable "sqlplus"
    args "sys/${sysPassword}@${dbUrl} as sysdba"
    args "@install_headless_with_trigger.sql"
    ignoreExitValue true
    workingDir "$buildDir/deps/utPLSQL/source"
    doLast {
        exec {
            executable "sqlplus"
            args "sys/${sysPassword}@${dbUrl} as sysdba"
            standardInput = new ByteArrayInputStream("GRANT INHERIT PRIVILEGES ON USER sys TO ut3;".getBytes())

        }
    }
}


task installTests(type: Exec ){
    dependsOn installTestFramework
    dependsOn dataMigrate
    workingDir "src/test"
    executable "sqlplus"
    args "${cwms_user}/\"${project.findProperty("flyway.password")}\"@${dbUrl}"
    args "@tests.sql"
    args cwms_office
    args cwms_schema_name
    args cwms_eroc + "hectest_up"
    args cwms_eroc
    doLast {
        project.mkdir(coverageDir)
        exec {
            executable "sqlplus"
            workingDir "src/test"
            args "sys/${sysPassword}@${dbUrl} as sysdba"
			args "@test_grants.sql"
            args "${cwms_eroc}cwmspd"
			args "${cwms_eroc}hectest_ro"
			args "${cwms_eroc}hectest_db"
			args "${cwms_eroc}hectest_pu"
			args "${cwms_eroc}hectest_up"
			args "${cwms_schema_name}"
            args "${cwms_eroc}hectest"
            args "${cwms_eroc}webtest"
            args "${cwms_eroc}hectest_multioffice"
        }
    }
}

task cleanTestData(type: Exec) {
    dependsOn installTests
    executable="utplsql"
    args "run"
	args "-p=${cwms_schema_name}.test_clean_all"
    args "-f=UT_COVERAGE_HTML_REPORTER"
        args "-o=${coverageDir}/index.html"
    args "-f=UT_COVERAGE_COBERTURA_REPORTER"
        args "-o=${coverageDir}/coverage_pd.xml"
    args "-f=UT_JUNIT_REPORTER"
        args "-o=$buildDir/tests_pd.xml"
	args "-f=UT_DOCUMENTATION_REPORTER"
    args "-d" // diagnostic info
    args "-D" // dbms_output
    args "-s" // terminal output even with o set
	args "${cwms_user}/\"${project.findProperty("flyway.password")}\"@${dbUrl}"
}

task loadTestData() {
    dependsOn installTests
    dependsOn cleanTestData
    doLast {
        println("Loading Test Clobs")
        exec {
            def dbUrlWithoutParams = dbUrl.split("\\?")[0];
            workingDir "${projectDir}/src/test/data/ratings"
            executable "sqlldr"
            args "${cwms_user}/\"${project.findProperty("flyway.password")}\"@${dbUrlWithoutParams}"
            args "control=xsl_test_clobs.ctl"
        }
    }

}

task testAsPdUser(type: Exec) {
    dependsOn installTests
    dependsOn loadTestData

    executable "utplsql"
    args "run"
	//args "-p=${cwms_schema_name}.test_cwms_rating,${cwms_schema_name}.test_lrts_updates,${cwms_schema_name}.test_probability_parameter,${cwms_schema_name}.test_cwms_loc,${cwms_schema_name}.test_cwms_ts,${cwms_schema_name}.test_cwms_util,${cwms_schema_name}.test_cwms_prop,${cwms_schema_name}.test_versioned_time_series,${cwms_schema_name}.test_update_ts_extents,${cwms_schema_name}.test_cwms_pool,${cwms_schema_name}.test_timeseries_snapping,${cwms_schema_name}.test_cwms_cat,${cwms_schema_name}.test_cwms_msg,${cwms_schema_name}.test_cwms_level,${cwms_schema_name}.test_cwms_display"
    args "-p=${cwms_schema_name}.test_cwms_loc,${cwms_schema_name}.test_cwms_ts"//,${cwms_schema_name}.test_cwms_util,${cwms_schema_name}.test_cwms_prop,${cwms_schema_name}.test_update_ts_extents,${cwms_schema_name}.test_cwms_display"
    args "-f=UT_COVERAGE_HTML_REPORTER"
        args "-o=${coverageDir}/index.html"
    args "-f=UT_COVERAGE_COBERTURA_REPORTER"
        args "-o=${coverageDir}/coverage_dba.xml"
    args "-f=UT_JUNIT_REPORTER"
        args "-o=$buildDir/tests_pdu.xml"
	args "-f=UT_DOCUMENTATION_REPORTER"
    args "-d" // diagnostic info
    args "-D" // dbms_output
    args "-s" // terminal output even with o set
	args "${cwms_eroc}cwmspd/\"${pd_password}\"@${dbUrl}"
}

task testAsRoUser(type: Exec ) {
    dependsOn installTests
    dependsOn loadTestData

    executable="utplsql"
    args "run"
	args "-p=${cwms_schema_name}.test_ro"
    args "-f=UT_COVERAGE_HTML_REPORTER"
        args "-o=${coverageDir}/index.html"
    args "-f=UT_COVERAGE_COBERTURA_REPORTER"
        args "-o=${coverageDir}/coverage_ro.xml"
    args "-f=UT_JUNIT_REPORTER"
        args "-o=$buildDir//tests_ro.xml"
	args "-f=UT_DOCUMENTATION_REPORTER"
    args "-d"
    args "-D"
    args "-s"
	args "${cwms_eroc}hectest_ro/\"${pd_password}\"@${dbUrl}"
}

task testMigrationCode(type: Test) {
    dependsOn compileJava
    dependsOn compileTestJava
    useJUnitPlatform()
}


task testDb() {
    dependsOn testMigrationCode
    dependsOn installTests

    dependsOn cleanTestData
    dependsOn loadTestData
    dependsOn testAsPdUser
    //dependsOn testAsRoUser
}



test.dependsOn testDb
